<<<<<<<< .../cleanup_more.d

/* rumors: tagg rumors about chapter 7 ongoings */

ADD_STATE_TRIGGER RBALDU 0 ~Global("C#st_HeroOfBG","GLOBAL",0)~
ADD_STATE_TRIGGER RBALDU 1 ~Global("C#st_HeroOfBG","GLOBAL",0)~
ADD_STATE_TRIGGER RBALDU 2 ~Global("C#st_HeroOfBG","GLOBAL",0)~
ADD_STATE_TRIGGER RBALDU 4 ~Global("C#st_HeroOfBG","GLOBAL",0)~
ADD_STATE_TRIGGER RBALDU 6 ~Global("C#st_HeroOfBG","GLOBAL",0)~
ADD_STATE_TRIGGER RBALDU 7 ~Global("C#st_HeroOfBG","GLOBAL",0)~
ADD_STATE_TRIGGER RBALDU 12 ~Global("C#st_HeroOfBG","GLOBAL",0)~
ADD_STATE_TRIGGER RBALDU 13 ~Global("C#st_HeroOfBG","GLOBAL",0)~
ADD_STATE_TRIGGER RBALDU 16 ~Global("C#st_HeroOfBG","GLOBAL",0)~
ADD_STATE_TRIGGER RBALDU 17 ~Global("C#st_HeroOfBG","GLOBAL",0)~


ADD_STATE_TRIGGER RBEREG 1 ~Global("C#st_HeroOfBG","GLOBAL",0)~
ADD_STATE_TRIGGER RBEREG 16 ~Global("C#st_HeroOfBG","GLOBAL",0)~
ADD_STATE_TRIGGER RBEREG 18 ~Global("C#st_HeroOfBG","GLOBAL",0)~


ADD_STATE_TRIGGER RFRIEN 1 ~Global("C#st_HeroOfBG","GLOBAL",0)~
ADD_STATE_TRIGGER RFRIEN 3 ~Global("C#st_HeroOfBG","GLOBAL",0)~
ADD_STATE_TRIGGER RFRIEN 4 ~Global("C#st_HeroOfBG","GLOBAL",0)~

/* new dialogues for commoners */

ADD_STATE_TRIGGER FTOWBAS 1 ~Global("C#st_HeroOfBG","GLOBAL",0)~
ADD_STATE_TRIGGER FTOWBAS 3 ~Global("C#st_HeroOfBG","GLOBAL",0)~

APPEND FTOWBAS
IF ~Global("C#st_HeroOfBG","GLOBAL",1)  RandomNum(4,2)
~ THEN BEGIN sarevok_dead_01 
  SAY @12 /* ~That crummy Sarevok was an evil one, yes he was! He murdered Scar, and Entar and tried to kill lots of others! He deserved death, and thanks to <CHARNAME> it came to him quickly!~ */
  IF ~~ THEN EXIT
END
IF ~Global("C#st_HeroOfBG","GLOBAL",1)  RandomNum(4,4)
~ THEN BEGIN sarevok_dead_02
  SAY @13 /* ~It's so frightening! To think that Sarevok came so close to completing his murderous plans. I am glad <CHARNAME> tracked him down and brought him to justice. Let that be an example to other crazies so they don't try to do what he did.~ */
  IF ~~ THEN EXIT
END
END //APPEND


ADD_STATE_TRIGGER NOBLBAS 1 ~Global("C#st_HeroOfBG","GLOBAL",0)~

APPEND NOBLBAS
IF ~Global("C#st_HeroOfBG","GLOBAL",1) RandomNum(4,2)
~ THEN BEGIN sarevok_dead_01 
  SAY @14 /* ~What Sarevok did was unforgivable! I am glad <CHARNAME> went after the scum, before he could do any more hurt.~ */
  IF ~~ THEN EXIT
END
END //APPEND

ADD_STATE_TRIGGER NOBWBAS 0 ~Global("C#st_HeroOfBG","GLOBAL",0)~
ADD_STATE_TRIGGER NOBWBAS 2 ~Global("C#st_HeroOfBG","GLOBAL",0)~
ADD_STATE_TRIGGER NOBWBAS 3 ~Global("C#st_HeroOfBG","GLOBAL",0)~

APPEND NOBWBAS

IF ~Global("C#st_HeroOfBG","GLOBAL",1) RandomNum(4,1)
~ THEN BEGIN sarevok_dead_01 
  SAY @15 /* ~Well, I never! Can you believe the nerve of that Sarevok fellow? After the trust we all placed in him, he turns out to be nothing but the basest of villains. I'm glad he was caught and punished.~ */
  IF ~~ THEN EXIT
END

IF ~Global("C#st_HeroOfBG","GLOBAL",1) RandomNum(4,3)
~ THEN BEGIN sarevok_dead_02
  SAY @16 /* ~Aren't you <CHARNAME>? It's pleasing to have a hero in the city! On behalf of everyone I know, I'd like to thank you for what you've done. If it weren't for your heroics... Well, I shudder to think how a war with Amn would have turned out. Thank you for catching that villainous Sarevok and putting an end to his trouble-making!~ */
  IF ~~ THEN EXIT
END

IF ~Global("C#st_HeroOfBG","GLOBAL",1) RandomNum(4,4)
~ THEN BEGIN sarevok_dead_03
  SAY @17 /* ~It's spreading like wildfire! Everyone's talking about what happened at the palace. From what I've heard, that Sarevok fellow wasn't as noble as he'd like us all to believe. He tried to have the other Dukes killed, so that he could take over the city.~ */
  IF ~~ THEN EXIT
END

END //APPEND

/* Benjy and Dilos in FF Headquarters: do not turn hostile after scene in Ducal Palace */
REPLACE_STATE_TRIGGER BENJY 0 ~Global("DukeThanks","GLOBAL",0)~
REPLACE_STATE_TRIGGER FLAMBG 0 ~OR(2) !Global("Chapter","GLOBAL",7) !Global("DukeThanks","GLOBAL",0)~
ADD_STATE_TRIGGER FLAMBG 5 ~Global("DukeThanks","GLOBAL",0)~

/* prevent journal entry about rising tension with Amn */
EXTEND_BOTTOM flambg 6
IF ~GlobalGT("DukeThanks","GLOBAL",0)~ THEN EXIT
END
 
CHAIN
IF WEIGHT #-1 ~GlobalGT("DukeThanks","GLOBAL",0)
Global("C#st_DilosThanks","MYAREA",0)~ THEN FLAMBG after_Sarevok
@52 /* ~Ah, <CHARNAME>! I am so relieved that the hubbub is over. It was a bad feeling, being ordered to fight you, after seeing how Duke Eltan trusted you the way he did before Angelo took over...~ */
== FLAMBG IF ~GlobalGT("BroughtEltan","GLOBAL",0)~ THEN @53 /* ~And thank you, *thank you* for saving our commander.~ */
== FLAMBG @54 /* ~We will mourn our losses while we gather what is left after Sarevok and his minions besmirched the Flaming Fist like they did, making us his tools. Thank you for saving this city, hero.~ */
END
IF ~~ THEN DO ~SetGlobal("C#st_DilosThanks","MYAREA",1)~ EXIT

APPEND BENJY
IF ~GlobalGT("DukeThanks","GLOBAL",0)~ THEN all_done
SAY @55 /* ~Seems fighting you wasn't the right thing to do after all. Well, who would have thought that Angelo and Sarevok would play such a wrong game. It seems you saved us from dark times, indeed.~ */
IF ~~ THEN EXIT
END
END //APPEND


>>>>>>>>
COMPILE EVALUATE_BUFFER ~.../cleanup_more.d~

/* NBaldursGate: Bill FF who checks the invitations */
<<<<<<<< .../ar0200_add.baf
IF
	Exists("BILL")  // Bill
	InMyArea("BILL")
	Dead("Sarevok")
THEN
	RESPONSE #100
		ActionOverride("BILL",DestroySelf())
END
>>>>>>>>
EXTEND_BOTTOM ~%NBaldursGate%.bcs~ ~.../ar0200_add.baf~

/* Benjy and Dilos in FF Headquarters: turn friendly after scene in Ducal Palace */
<<<<<<<< .../ar0706_add.baf
IF
	Exists("BENJY")  // Benjy
	GlobalGT("DukeThanks","GLOBAL",0)
	Allegiance("BENJY",ENEMY)
THEN
	RESPONSE #100
		ActionOverride("BENJY",ChangeEnemyAlly(Myself,NEUTRAL))
END

IF
	Exists("DICKSMOK")  // Dilos
	GlobalGT("DukeThanks","GLOBAL",0)
	Allegiance("DICKSMOK",ENEMY)
THEN
	RESPONSE #100
		ActionOverride("DICKSMOK",ChangeEnemyAlly(Myself,NEUTRAL))
END
>>>>>>>>
EXTEND_BOTTOM ~%SWBaldursGate_FlamingFistHQ_L1%.bcs~ ~.../ar0706_add.baf~

/* in case FLAMANG.cre was spawned in Flaming Fist HQ (hostile FF mercenaries): make them despawn after Sarevok is demasked */
COPY_EXISTING ~FLAMANG.cre~ ~override~
  WRITE_EVALUATED_ASCII 0x260 ~c#stdst2~ #8 // General Script 

/* compatibility with Jarl's Adventure Pack - JA#IGNAT.cre doesn't have a DV */
ACTION_IF FILE_EXISTS_IN_GAME ~JA#IGNAT.dlg~ THEN BEGIN
  COPY_EXISTING ~JA#IGNAT.cre~ ~override~
    WRITE_EVALUATED_ASCII 0x260 ~c#stdstr~ #8 // General Script 
END

/* compatibility with Jarl's Adventure Pack - Nila */
ACTION_IF FILE_EXISTS_IN_GAME ~JA#NILA.bcs~ THEN BEGIN
  EXTEND_BOTTOM ~JA#NILA.bcs~ ~override/c#stdstr.bcs~
END


/* afte Sarevok is dead: Duke Eltan is no longer searched for, so the Harbormaster shouldn't refer to him. */

<<<<<<<< ...inlined/harbor_eltan.d
ADD_STATE_TRIGGER HARBOR 10 ~OR(2) Dead("Sarevok")~ UNLESS ~Dead("Sarevok")~
ADD_STATE_TRIGGER HARBOR 11 ~!Dead("Sarevok")~
>>>>>>>>
COMPILE EVALUATE_BUFFER ~...inlined/harbor_eltan.d~


/* Hafiz */

/* preparing patching of Hafiz: use correct reply option. For BGT I'll just assume the state was not altered because I don't know the transaction's str.ref number. */
ACTION_IF NOT GAME_IS ~bgt~ BEGIN
// List of all potential response strrefs to check
ACTION_DEFINE_ASSOCIATIVE_ARRAY response_strrefs BEGIN
  ~%eet_2%18345~ => 1
END
  
COPY_EXISTING ~HAFIZ.dlg~ ~override~
  // scanning listed dialog states
  PATCH_FOR_EACH state IN 3 BEGIN
    LPF GET_RESPONSE_STRREFS INT_VAR state RET strrefs RET_ARRAY strrefs END
    TEXT_SPRINT indices ~~  // a temporary variable for building the index list
    FOR (i = 0; i < strrefs; ++i) BEGIN
      SET value = $strrefs(~%i%~)
      // Include index only if strref is listed in the response_strrefs array
      PATCH_IF (VARIABLE_IS_SET $response_strrefs(~%value%~)) BEGIN
        TEXT_SPRINT indices ~%indices% %i%~ // building list of indices
      END
    END
    // prevent adding a trigger if index list is empty
    PATCH_IF (~%indices%~ STR_EQ ~~) BEGIN
      TEXT_SPRINT indices "IF ~False()~"
    END
    // initialize variables responses_3
    // EVAL is used to create the variable name dynamically
    TEXT_SPRINT EVAL ~responses_%state%~ ~%indices%~
    PATCH_PRINT ~Variable %state%: %indices%~
  END
BUT_ONLY
END ELSE BEGIN
OUTER_SET responses_3 = 2
END

/* PC should not talk like Sarevok is still out there, looking for him */
<<<<<<<< ...inlined/hafiz_cleanup.d
/* #18345 ~There's quite a bit to say, I don't really know if I have the time to go into all of the details. I used to live at Candlekeep with my father Gorion. A little while back we were forced to flee. Seems like there's someone out there who really wants me dead. In trying to kill me, he... killed my dad.~ */
ADD_TRANS_TRIGGER HAFIZ 3 ~!Dead("Sarevok")~ DO %responses_3% //2

//18345 changed
EXTEND_BOTTOM HAFIZ 3
+ ~Dead("Sarevok")~ + @50 /* ~There's quite a bit to say, I don't really know if I have the time to go into all of the details. I used to live at Candlekeep with my father Gorion. A little while back we were forced to flee. Someone was out there who really wanted me dead. In trying to kill me, he... killed my dad.~ */ + 6_altered
END

//18351 changed
APPEND HAFIZ
IF ~~ THEN 6_altered
  SAY @51 /* ~I am sorry to bring back such painful memories. I wish to help you, and in doing so, end these dreams that plague me. Here, take this. Goodbye now. Perhaps we shall meet again.~ */
COPY_TRANS HAFIZ 6
END
END //APPEND
>>>>>>>>
COMPILE EVALUATE_BUFFER ~...inlined/hafiz_cleanup.d~

