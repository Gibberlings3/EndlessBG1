/* basic changes */

/* prevent end of game / SoD transition */
ACTION_IF GAME_IS ~bgee~ THEN BEGIN
  COPY_EXISTING ~%Undercity_TempleofBhaal%.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~Dead("Sarevok")[%newline%]*GlobalLT("EndOfBG1","GLOBAL",2)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~False()~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
  BUT_ONLY
END

ACTION_IF GAME_IS ~eet~ THEN BEGIN
  COPY_EXISTING ~%Undercity_TempleofBhaal%.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~Dead("Sarevok")[%newline%]*Global("EndOfBG1","GLOBAL",0)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~False()~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
  BUT_ONLY
END

/* end movie should play after Sarevok is dead */
<<<<<<<< .../ar0125-et.baf

IF
	Dead("Sarevok")  // Armored Figure (Sarevok)
	OR(2)
		Dead("GALDOR")   //Angelo
		!Exists("GALDOR")   //Angelo
	OR(2)
		Dead("SEMAJ")    // Semaj
		!Exists("SEMAJ")    // Semaj
	OR(2)
		Dead("Tazok")    //Tazok
		!Exists("Tazok")    //Tazok
	Global("C#st_BG1End","GLOBAL",0)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SaveGame(0)
		SmallWait(1)
		ClearAllActions()
		StartCutSceneMode()
		SetGlobal("C#st_BG1End","GLOBAL",1)
		SetGlobal("SarevokBehavior","GLOBAL",5)
		TriggerActivation("Door0123",TRUE)
		AddJournalEntry(@10012,QUEST_DONE)
		AddJournalEntry(@10013,QUEST_DONE)
		AddJournalEntry(@10014,QUEST_DONE)
		AddJournalEntry(@10015,QUEST_DONE)
		Wait(2)
		FadeToColor([30.0],0)
		Wait(2)
		StartMovie("ENDMOVIE")
		FadeFromColor([30.0],0)
		EndCutSceneMode()
		SetInterrupt(TRUE)
END

>>>>>>>>
EXTEND_TOP ~%Undercity_TempleofBhaal%.BCS~ ~.../ar0125-et.baf~ EVALUATE_BUFFER

/* more journal entries for EE */
ACTION_IF GAME_IS ~bgee eet~ THEN BEGIN
<<<<<<<< .../ar0125_ee.baf
IF
	Dead("Sarevok")  // Armored Figure (Sarevok)
	OR(2)
		Dead("GALDOR")   //Angelo
		!Exists("GALDOR")   //Angelo
	OR(2)
		Dead("SEMAJ")    // Semaj
		!Exists("SEMAJ")    // Semaj
	OR(2)
		Dead("Tazok")    //Tazok
		!Exists("Tazok")    //Tazok
	Global("C#st_BG1EndJournalEE","MYAREA",0)
THEN
	RESPONSE #100
		EraseJournalEntry(%eet_2%31475) 
		AddJournalEntry(@10016,QUEST)
		SetGlobal("C#st_BG1EndJournalEE","MYAREA",1)
END
>>>>>>>>
EXTEND_BOTTOM ~%Undercity_TempleofBhaal%.BCS~ ~.../ar0125_ee.baf~ EVALUATE_BUFFER
END


/* BG:EE: game ends - new cutscene */
ACTION_IF (GAME_IS ~bgee~ AND NOT FILE_EXISTS_IN_GAME ~bd0103.are~) THEN BEGIN

<<<<<<<< .../c#stbgee.baf

IF
	GlobalLT("EndOfBG1","GLOBAL",2)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
        	CutSceneId(Player1)
		SaveGame(2)
		SmallWait(1)
		SetGlobal("EndOfBG1","GLOBAL",2)
		Wait(4)
		FadeToColor([1.0],0)
		Wait(1)
		StartMovie("endcrdit")
		FadeToColor([1.0],0)
		EndCutSceneMode()
		GoToStartScreen()
		SetInterrupt(TRUE)
END

>>>>>>>>

COMPILE EVALUATE_BUFFER ~.../c#stbgee.baf~

END //BG:EE

/* SoD + EET: Transition to SoD - new cutscene */
ACTION_IF ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~)) THEN BEGIN

<<<<<<<< .../c#st2sod.baf

IF
	True()
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
        	CutSceneId(Player1)
		FadeToColor([1.0],0)
		SaveGame(2)
		SmallWait(1)
		StartCutSceneEx("BDSODTRN",TRUE)
		SetInterrupt(TRUE)
END

>>>>>>>>

COMPILE EVALUATE_BUFFER ~.../c#st2sod.baf~

/* SoD + EET: patch bdsodtrn.bcs so the BG1 Endmovie doesn't show twice */

COPY_EXISTING ~bdsodtrn.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~Wait(1)[%newline%]*StartMovie("ENDMOVIE")[%newline%]*FadeToColor(\[1\.0\],0)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

END //SoD + EET

/* EET: direct transition to BGII - cutscene into bd6100.are for technical reasons */
ACTION_IF GAME_IS ~eet~ THEN BEGIN
<<<<<<<< .../c#ebg122.baf

IF
	!AreaCheck("bd6100")
THEN
	RESPONSE #100
		CutSceneId(Player1)
		FadeToColor([40.0],0)
		DisableAI(Player1,TRUE)
		DisableAI(Player2,TRUE)
		DisableAI(Player3,TRUE)
		DisableAI(Player4,TRUE)
		DisableAI(Player5,TRUE)
		DisableAI(Player6,TRUE)
		SmallWait(5)
		ActionOverride(Player2,LeaveAreaLUA("bd6100","",[535.340],SE))
		ActionOverride(Player3,LeaveAreaLUA("bd6100","",[565.280],SE))
		ActionOverride(Player4,LeaveAreaLUA("bd6100","",[505.300],SE))
		ActionOverride(Player5,LeaveAreaLUA("bd6100","",[535.230],SE))
		ActionOverride(Player6,LeaveAreaLUA("bd6100","",[470.270],SE))
		LeaveAreaLUAPanic("bd6100","",[600.330],SE)
		LeaveAreaLUA("bd6100","",[600.330],SE)
		MultiPlayerSync()
		Wait(1)
		SetGlobal("bd_finale","bd6100",6)
		EndCutSceneMode()
		CreateCreatureObject("K#TELBGT",Player1,0,0,0)
END

>>>>>>>>

COMPILE EVALUATE_BUFFER ~.../c#ebg122.baf~


/* make sure Imoen, Minsk and Jaheira do not have their BG1 equipment at the beginning of BGII */
COPY_EXISTING ~K#TELBGT.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(ActionOverride("K#ImportContainer",TakeCreatureItems(Player6,ALL))\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1
ActionOverride("K#ImportContainer",TakeCreatureItems("Imoen2",ALL))
ActionOverride("K#ImportContainer",TakeCreatureItems("Jaheira",ALL))
ActionOverride("K#ImportContainer",TakeCreatureItems("Minsc",ALL))~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY
END //eet


/* N Baldurs Gate: open entrance to Palace */
<<<<<<<< .../ar0200_add.baf
IF
	Dead("Sarevok")
	Global("C#st_SarevokDeadOpenGate","MYAREA",0)
THEN
	RESPONSE #100
		OpenDoor("Door0108")
		TriggerActivation("Dor0108",TRUE)
		SetGlobal("C#st_SarevokDeadOpenGate","MYAREA",1)
END
>>>>>>>>
EXTEND_BOTTOM ~%NBaldursGate%.bcs~ ~.../ar0200_add.baf~ EVALUATE_BUFFER 


/* prepare the Palace (ground floor) after Sarevok's death */

/* remove any nobles who might not have heard the call */
<<<<<<<< .../sarrun_add.baf
IF
	Dead("Sarevok")
THEN
	RESPONSE #100
		DestroySelf()
END
>>>>>>>>
EXTEND_BOTTOM ~SARRUN.bcs~ ~.../sarrun_add.baf~ EVALUATE_BUFFER 

/* Palace (ground floor): Duke Belt should be alive */
<<<<<<<< .../ar0108_add.baf
/* Palace ground floor. revive Duke Belt and move him in the middle of the room */

IF
	Dead("Sarevok")
	Dead("BELT")
	Global("C#st_RearrangeDukes","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("SPRITE_IS_DEADBELT","GLOBAL",0)
		CreateCreature("BELT",[735.371],S)
		SmallWait(1)
		Continue()
END

IF
	Dead("Sarevok")
	Global("C#st_RearrangeDukes","MYAREA",0)
THEN
	RESPONSE #100
		ApplySpellRES("%bddispel%","BELT")  
		SmallWait(1)
		ActionOverride("BELT",JumpToPoint([735.371]))
		ActionOverride("BELT",Face(0))
		SmallWait(1)
		Continue()
END

IF
	Dead("Sarevok")
	Global("C#st_RearrangeDukes","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("C#st_RearrangeDukes","MYAREA",1)
END

/* Remove Duke Eltan's Body in case the player didn't bring him to the harbor master */
IF
	PartyHasItem("MISC55")
	Global("C#st_RemoveMisc55","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("BD0120_START","GLOBAL",1)
		SetGlobal("BDAI_RESET_TIMERS","GLOBAL",1)
		ActionOverride(Player1,DestroyItem("MISC55"))
		ActionOverride(Player2,DestroyItem("MISC55"))
		ActionOverride(Player3,DestroyItem("MISC55"))
		ActionOverride(Player4,DestroyItem("MISC55"))
		ActionOverride(Player5,DestroyItem("MISC55"))
		ActionOverride(Player6,DestroyItem("MISC55"))
		SetGlobal("C#st_RemoveMisc55","MYAREA",1)
END
>>>>>>>>
EXTEND_BOTTOM ~%NBaldursGate_DucalPalace_L1%.bcs~ ~.../ar0108_add.baf~ EVALUATE_BUFFER 

/* define script for cres without DV: DestroySelf() after Sarevok's death */
<<<<<<<< .../c#stdstr.baf

IF
	GlobalGT("SarevokBehavior","GLOBAL",0)
	OR(2)
	  Dead("Sarevok")
	  !AreaCheck("%ThievesMaze%")
THEN
	RESPONSE #100
        	DestroySelf()
END

>>>>>>>>

COMPILE EVALUATE_BUFFER ~.../c#stdstr.baf~

/* define script for cres without DV: DestroySelf() after Sarevok's demasking in the Ducal Palace */
<<<<<<<< .../c#stdst2.baf

IF
	GlobalGT("DukeThanks","GLOBAL",0)
THEN
	RESPONSE #100
        	DestroySelf()
END

>>>>>>>>

COMPILE EVALUATE_BUFFER ~.../c#stdst2.baf~

/* detection variable for other mods (e.g. NPC mods) that EBG1 will kick in after Sarevok's death. Will be set in Thieves Guild after the Palace Fight */
<<<<<<<< .../ar0146_detection_add.baf
IF
	Global("C#EndlessBG1","GLOBAL",0)
	GlobalGT("SarevokBehavior","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#EndlessBG1","GLOBAL",1)
END
>>>>>>>>
EXTEND_BOTTOM ~%EBaldursGate_ThievesGuild%.bcs~ ~.../ar0146_detection_add.baf~ EVALUATE_BUFFER 

/* give winski2.cre DV (same as Bg1NPC Project) */
COPY_EXISTING ~winski2.cre~ ~override~
  WRITE_ASCII 0x280 ~winski2~ #32



/* erase open journal entries */

EXTEND_BOTTOM ~%NBaldursGate_DucalPalace_L1%.bcs~ ~c#endlessbg1/maincomponent/erasejournalentries.baf~ EVALUATE_BUFFER 
