
////////////////////////////////////////////////////////////////////////
//////
//////  Endless BG1 
//////   by jastey
////////////////////////////////////////////////////////////////////////

BACKUP ~c#endlessbg1/backup~
AUTHOR ~https://www.gibberlings3.net/forums/~ //jastey

VERSION ~17~

README ~c#endlessbg1/readme.c#endlessbg1.%LANGUAGE%.txt~ ~c#endlessbg1/readme.c#endlessbg1.english.txt~

AUTO_TRA ~c#endlessbg1/translations/%s~

ALWAYS

/* check for a ready SoD */

ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) THEN BEGIN
  FAIL @1002 /* ~Modmerge or Argent's DLC Merger is required before mods can be installed on this game.~ */
END

  OUTER_SPRINT newline ~%WNL%%LNL%%MNL%%TAB% ~
  INCLUDE ~c#endlessbg1/lib/extra_regexp_vars.tph~
  INCLUDE ~c#endlessbg1/lib/get_respone_sstrrefs.tph~

/* read in general definitions */
ACTION_IF GAME_IS ~bgee~ THEN BEGIN
  INCLUDE ~c#endlessbg1/lib/g3_bgee_cpmvars.tpa~
END

ACTION_IF GAME_IS ~eet~ THEN BEGIN
  INCLUDE ~EET/other/cpmvars/eet_cpmvars.tpa~
END

ACTION_IF GAME_IS ~bgt~ THEN BEGIN
  INCLUDE ~c#endlessbg1/lib/g3_bgt_cpmvars.tpa~
END

/* custom definitions: bgee */
ACTION_IF (GAME_IS ~bgee~ AND NOT FILE_EXISTS_IN_GAME ~bd0103.are~) THEN BEGIN
 OUTER_SPRINT ~bgee_only~ ~~
 OUTER_SPRINT ~sod~ ~False()~
 OUTER_SPRINT ~eet_only~ ~False()~
 OUTER_SPRINT ~start_bg1end_sod_cutscene~ ~ClearAllActions()
		StartCutSceneMode()
		StartCutScene("c#stbgee")~
 OUTER_SPRINT ~move_to_bg2~ ~~
 OUTER_SPRINT ~bdresurr~ ~sppr712~ //resurrection
 OUTER_SPRINT ~bddispel~ ~bpdispel~
 OUTER_SPRINT ~clearcld~ ~sppr318~
 OUTER_SPRINT ~BELT~ ~BELT~
  OUTER_SET BGTIMOENPState0 = 0 
  OUTER_SET BGTIMOENPState4 = 4
  OUTER_SPRINT ~eet_2~ ~~
  OUTER_SPRINT ~eet_0~ ~~
OUTER_SET eet_200000 = GAME_IS ~eet~ ? 200000 : 0
END

/* custom definitions: sod */
ACTION_IF (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~) THEN BEGIN
 OUTER_SPRINT ~bgee_only~ ~False()~
 OUTER_SPRINT ~sod~ ~~
 OUTER_SPRINT ~eet_only~ ~False()~
 OUTER_SPRINT ~start_bg1end_sod_cutscene~ ~ClearAllActions()
		StartCutSceneMode()
		StartCutScene("c#st2sod")~
 OUTER_SPRINT ~move_to_bg2~ ~~
 OUTER_SPRINT ~bdresurr~ ~bdresurr~
 OUTER_SPRINT ~bddispel~ ~bddispel~
 OUTER_SPRINT ~clearcld~ ~clearcld~
 OUTER_SPRINT ~BELT~ ~BELT~
  OUTER_SET BGTIMOENPState0 = 0 
  OUTER_SET BGTIMOENPState4 = 4
  OUTER_SPRINT ~eet_2~ ~~
  OUTER_SPRINT ~eet_0~ ~~
OUTER_SET eet_200000 = GAME_IS ~eet~ ? 200000 : 0
END

/* custom definitions: eet */
ACTION_IF (GAME_IS ~eet~) THEN BEGIN
 OUTER_SPRINT ~bgee_only~ ~False()~
 OUTER_SPRINT ~sod~ ~~
 OUTER_SPRINT ~eet_only~ ~~
 OUTER_SPRINT ~start_bg1end_sod_cutscene~ ~ClearAllActions()
		StartCutSceneMode()
		StartCutScene("c#st2sod")~
 OUTER_SPRINT ~move_to_bg2~ ~ClearAllActions()
SetGlobal("ENDOFBG1","GLOBAL",1)
SetGlobal("C#EBG1_DirectBG2Transition","GLOBAL",1)
//CreateCreatureObject("K#TELBGT",Player1,0,0,0)
StartCutSceneMode()
StartCutSceneEx("c#ebg122",TRUE)~
 OUTER_SPRINT ~bdresurr~ ~bdresurr~
 OUTER_SPRINT ~bddispel~ ~bddispel~
 OUTER_SPRINT ~clearcld~ ~clearcld~
 OUTER_SPRINT ~BELT~ ~BELT~
  OUTER_SET BGTIMOENPState0 = 0 
  OUTER_SET BGTIMOENPState4 = 4
  OUTER_SPRINT ~eet_2~ ~2~
  OUTER_SPRINT ~eet_0~ ~0~
OUTER_SET eet_200000 = GAME_IS ~eet~ ? 200000 : 0
END

/* custom definitions: bgt */
ACTION_IF GAME_IS ~bgt~ THEN BEGIN
 OUTER_SPRINT ~bgee_only~ ~False()~
 OUTER_SPRINT ~sod~ ~False()~
 OUTER_SPRINT ~eet_only~ ~False()~
 OUTER_SPRINT ~start_bg1end_sod_cutscene~ ~~
 OUTER_SPRINT ~move_to_bg2~ ~~
 OUTER_SPRINT ~bdresurr~ ~sppr712~ //resurrection
 OUTER_SPRINT ~bddispel~ ~spin866~
 OUTER_SPRINT ~clearcld~ ~sppr318~
 OUTER_SPRINT ~BELT~ ~BELTBRD~
 OUTER_SPRINT ~belt_victory~ ~0~
  OUTER_SET BGTIMOENPState0 = 16 
  OUTER_SET BGTIMOENPState4 = 20
END
//////////////////////////////////////////////////////////////////////
/* all following actions are only processed ONCE for the whole mod, independent of un- and reinstalling of single components */

ACTION_IF !FILE_EXISTS ~c#endlessbg1/install/c#endlessbg1install.mrk~ BEGIN

  ACTION_DEFINE_ARRAY c#noconvert BEGIN setup END
  ACTION_DEFINE_ARRAY c#reload BEGIN game journal END

  LAF HANDLE_CHARSETS
    INT_VAR
      infer_charsets = 1
    STR_VAR
      tra_path = EVAL ~c#endlessbg1/Translations~
      noconvert_array = c#noconvert
      reload_array = c#reload
  END

//CamDawgs CD_STATE_NOTVALID 
    APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~

  COPY ~c#endlessbg1/Install/component.xx~ ~c#endlessbg1/install/c#endlessbg1install.mrk~

  END // c#endlessbg1install.mrk

 //reloading of tras:
  LOAD_TRA ~c#endlessbg1/Translations/english/game.tra~
  LOAD_TRA ~c#endlessbg1/Translations/%LANGUAGE%/game.tra~
ACTION_IF (GAME_IS ~bgee eet~) THEN BEGIN
  LOAD_TRA ~c#endlessbg1/Translations/english/game_ee.tra~
  LOAD_TRA ~c#endlessbg1/Translations/%LANGUAGE%/game_ee.tra~
END //bgee eet
  LOAD_TRA ~c#endlessbg1/Translations/english/journal.tra~
  LOAD_TRA ~c#endlessbg1/Translations/%LANGUAGE%/journal.tra~

END //ALWAYS


LANGUAGE ~English~
         ~english~   
         ~c#endlessbg1/Translations/english/setup.tra~
	 ~c#endlessbg1/Translations/english/game.tra~
	 ~c#endlessbg1/Translations/english/journal.tra~

LANGUAGE ~Deutsch~
         ~german~   
         ~c#endlessbg1/Translations/german/setup.tra~
	 ~c#endlessbg1/Translations/german/game.tra~
	 ~c#endlessbg1/Translations/german/journal.tra~

LANGUAGE ~Russian (by Austin, Arkie & Arcanecoast.ru)~
         ~russian~   
         ~c#endlessbg1/Translations/english/setup.tra~
	 ~c#endlessbg1/Translations/russian/game.tra~
	 ~c#endlessbg1/Translations/russian/journal.tra~

LANGUAGE ~Polski~
         ~polish~   
         ~c#endlessbg1/Translations/polish/setup.tra~
	 ~c#endlessbg1/Translations/polish/game.tra~
	 ~c#endlessbg1/Translations/polish/journal.tra~

LANGUAGE ~Francais~
         ~french~   
         ~c#endlessbg1/Translations/french/setup.tra~
	 ~c#endlessbg1/Translations/french/game.tra~
	 ~c#endlessbg1/Translations/french/journal.tra~

LANGUAGE ~Italiano~
         ~italian~   
         ~c#endlessbg1/Translations/italian/setup.tra~
	 ~c#endlessbg1/Translations/italian/game.tra~
	 ~c#endlessbg1/Translations/italian/journal.tra~

LANGUAGE ~Spanish~
         ~spanish~   
         ~c#endlessbg1/Translations/spanish/setup.tra~
	 ~c#endlessbg1/Translations/spanish/game.tra~
	 ~c#endlessbg1/Translations/spanish/journal.tra~

////////////////////////////////////////////////////////////////////////
//////
BEGIN @5001 /* ~Endless BG1: Main Component (Required)~ */ DESIGNATED 0 
LABEL EBG1_MainComponent
REQUIRE_PREDICATE (GAME_IS ~eet bgee bgt~) @5000 /* ~This mod is only compatible with BG:EE, SoD, BGT, and EET.~ */
FORBID_COMPONENT ~transitions/setup-transitions.tp2~ 0 @5021 /* ~ebg1 needs to be installed before Transitions.~ */
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5023
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5023

/* main component: required */
/* Content for EE: 
-prevent end of game/transition to SoD after Sarevok's death
-Duke Belt will be alive and in the Palace
-adds basic dialogue to Duke Belt and Duke Jannath (if Jannath is still alive)
-erase references to Sarevok as new Duke etc. in dialogues of city folk after his death
-door to Palace will be open after Sarevok's death

For BGT, this adds some lines to Belt's dialogue and more cleanup of references to Sarevok in dialogues after his death
 */

/* add main dialogue to Duke Jannath - makes no real sense here for bgt but I need this later */
COMPILE EVALUATE_BUFFER ~c#endlessbg1/maincomponent/dialogues.d~

ACTION_IF GAME_IS ~bgee eet~ THEN BEGIN
  /* prevent end of BG1, open up Palace etc. */
  INCLUDE ~c#endlessbg1/maincomponent/general.tpa~
  /* "clean up" of Sarevok references (is already included in BGT or not relevant) */
  INCLUDE ~c#endlessbg1/maincomponent/sarevok_cleanup.tpa~
  /* more cleanup not yet considered in BGT */
  INCLUDE ~c#endlessbg1/maincomponent/sarevok_cleanup_more.tpa~
  /* add dialogues to Duke Belt and Jannath */
  COMPILE EVALUATE_BUFFER ~c#endlessbg1/maincomponent/dialogues_ee.d~ USING ~c#endlessbg1/translations/%s/dialogues.tra~
END

ACTION_IF GAME_IS ~bgt~ THEN BEGIN
  /* more cleanup not yet considered in BGT */
  INCLUDE ~c#endlessbg1/maincomponent/sarevok_cleanup_more.tpa~
/* I put the erase journals here so I don't have to split and add several script blocks. */
  INCLUDE ~c#endlessbg1/maincomponent/journalerase_bgt.tpa~
END


/* ##

/* make sure Imoen talks with the correct dialogue after joining the group at the end of SoD */
COPY_EXISTING ~bdcut65.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(ActionOverride("IMOEN2?",\)StartDialogNoSet(Player1))~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 StartDialogOverride("bdimoen",Player1)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY
*/


////////////////////////////////////////////////////////////////////////
//////
BEGIN @5002   /* ~More Flavor to Hero of Baldur's Gate (Includes PC's Residence Inside Palace)~ */ DESIGNATED 1 
LABEL EBG1_MoreFlavor
REQUIRE_COMPONENT ~c#endlessbg1.tp2~ ~0~ @5003
REQUIRE_PREDICATE (GAME_IS ~eet bgee bgt~) @5013 /* ~This component is only compatible with BG:EE, SoD, BGT, and EET.~ */
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5023
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5023

ADD_JOURNAL @10002 @10003 USING ~c#endlessbg1/translations/%s/dialogues.tra~

INCLUDE ~c#endlessbg1/pc_palaceresidence/pc_palaceresidence.tpa~

COMPILE EVALUATE_BUFFER ~c#endlessbg1/pc_palaceresidence/pc_palaceresidence.d~ USING ~c#endlessbg1/translations/%s/dialogues.tra~

ACTION_IF GAME_IS ~bgee eet~ THEN BEGIN
// Get state for BELT %belt_victory_01% (in dialogues_ee.d)
/* ~You opened our eyes to the deceit and saved us from a great peril while we remained blind to the threat. By the power bestowed upon me as Duke of the Council of Four of this city I herewith name you "Hero of Baldur's Gate"! In the name of the city let me express our deepest gratitude!~
*/
OUTER_SET belt_victory_01 = STATE_WHICH_SAYS 1 IN ~c#endlessbg1/translations/%s/dialogues.tra~ FROM ~BELT~

/* ee: add announcement of PC's quarters inside the palace */
  COMPILE EVALUATE_BUFFER ~c#endlessbg1/pc_palaceresidence/add_ee.d~ USING ~c#endlessbg1/translations/%s/dialogues.tra~
END //bgee, eet


/* bgt: add more flavor to Belt's dialogue */
ACTION_IF GAME_IS ~bgt~ THEN BEGIN
  COMPILE EVALUATE_BUFFER ~c#endlessbg1/pc_palaceresidence/ceremony_bgt.d~ USING ~c#endlessbg1/translations/%s/dialogues.tra~
END //bgt


////////////////////////////////////////////////////////////////////////
//////
BEGIN @5018 /* Short Public Hero Tribute */ DESIGNATED 2 
LABEL EBG1_HeroCutscene
REQUIRE_COMPONENT ~c#endlessbg1.tp2~ ~0~ @5003
REQUIRE_COMPONENT ~c#endlessbg1.tp2~ ~1~ @5019
REQUIRE_PREDICATE ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~)) @5012 /* ~This mod is only compatible with SoD and EET.~ */
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5023
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5023

// Get state for BELT %victory% (in dialogues_ee.d)
/* ~<CHARNAME>, you were successful! You defeated Sarevok who conspired against the city, the Sword Coast, even Amn - for his own dark goals and purpose.~ */
OUTER_SET belt_victory = STATE_WHICH_SAYS 0 IN ~c#endlessbg1/translations/%s/dialogues.tra~ FROM ~BELT~

// Get state for BELT %victory_01% (in dialogues_ee.d)
/* ~You opened our eyes to the deceit and saved us from a great peril while we remained blind to the threat. By the power bestowed upon me as Duke of the Council of Four of this city I herewith name you "Hero of Baldur's Gate"! In the name of the city let me express our deepest gratitude!~
*/
OUTER_SET belt_victory_01 = STATE_WHICH_SAYS 1 IN ~c#endlessbg1/translations/%s/dialogues.tra~ FROM ~BELT~

// Get state for BELT %victory_42% (in add_ee.d)
/* ~You opened our eyes to the deceit and saved us from a great peril while we remained blind to the threat. By the power bestowed upon me as Duke of the Council of Four of this city I herewith name you "Hero of Baldur's Gate"! In the name of the city let me express our deepest gratitude!~
*/
OUTER_SET belt_victory_42 = STATE_WHICH_SAYS 42 IN ~c#endlessbg1/translations/%s/dialogues.tra~ FROM ~BELT~

  COMPILE EVALUATE_BUFFER ~c#endlessbg1/hero_cutscene/ceremony_sod.d~ USING ~c#endlessbg1/translations/%s/dialogues.tra~

COMPILE EVALUATE_BUFFER ~c#endlessbg1/hero_cutscene/c#stcut1.baf~
COMPILE EVALUATE_BUFFER ~c#endlessbg1/hero_cutscene/c#stcut2.baf~

/* Duke Belt for cutscene */
  COPY_EXISTING ~BELT.cre~ ~override/C#STBELT.cre~
    WRITE_EVALUATED_ASCII 0x248 ~~ #8   // Override Skript

/* area for celebration - bd0035 reused (SoD/EET only) */
  COPY ~c#endlessbg1/hero_cutscene/c#st35.are~ ~override~



////////////////////////////////////////////////////////////////////////
//////
BEGIN @5008 /* ~Sarevok's Unique Items~ */ DESIGNATED 3 
LABEL EBG1_SarevokItems
REQUIRE_COMPONENT ~c#endlessbg1.tp2~ ~0~ @5003
FORBID_COMPONENT ~transitions/setup-transitions.tp2~ 20 @5022 /* ~This component of ebg1 is already replaced by Transitions.~ */
FORBID_COMPONENT ~transitions/setup-transitions.tp2~ 21 @5022 /* ~This component of ebg1 is already replaced by Transitions.~ */
REQUIRE_PREDICATE (GAME_IS ~eet bgee bgt~) @5013 /* ~This component is only compatible with BG:EE, SoD, BGT, and EET.~ */
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5023
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5023

INCLUDE ~c#endlessbg1/sarevok_items/sarevok_items.tpa~




////////////////////////////////////////////////////////////////////////
//////
BEGIN @5014 /* ~Sarevok's Sword~ */ DESIGNATED 4 
LABEL EBG1_SarevokSword
REQUIRE_COMPONENT ~c#endlessbg1.tp2~ ~0~ @5003
FORBID_COMPONENT ~transitions/setup-transitions.tp2~ 20 @5022 /* ~This component of ebg1 is already replaced by Transitions.~ */
FORBID_COMPONENT ~transitions/setup-transitions.tp2~ 21 @5022 /* ~This component of ebg1 is already replaced by Transitions.~ */
REQUIRE_PREDICATE (GAME_IS ~eet bgee bgt~) @5013 /* ~This component is only compatible with BG:EE, SoD, BGT, and EET.~ */
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5023
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5023

ADD_JOURNAL TITLE (@10009) @10001 @10008 @10010 @10011 USING ~c#endlessbg1/translations/%s/dialogues.tra~

INCLUDE ~c#endlessbg1/sarevok_sword/sarevok_sword.tpa~

ACTION_IF GAME_IS ~bgee eet~ THEN BEGIN
// Get state for BELT %victory_01% (in dialogues_ee.d)
/* ~You opened our eyes to the deceit and saved us from a great peril while we remained blind to the threat. By the power bestowed upon me as Duke of the Council of Four of this city I herewith name you "Hero of Baldur's Gate"! In the name of the city let me express our deepest gratitude!~
*/
OUTER_SET belt_victory_01 = STATE_WHICH_SAYS 1 IN ~c#endlessbg1/translations/%s/dialogues.tra~ FROM ~BELT~
// Get state for BELT %victory_02% (in dialogues_ee.d)
/* ~<CHARNAME>, Hero of Baldur's Gate. Are you ready to combine forces with the Flaming Fist and go against the last followers of Sarevok?~ */
OUTER_SET belt_victory_02 = STATE_WHICH_SAYS 2 IN ~c#endlessbg1/translations/%s/dialogues.tra~ FROM ~BELT~
  COMPILE EVALUATE_BUFFER ~c#endlessbg1/sarevok_sword/dialogues_sarevoksword_ee.d~ USING ~c#endlessbg1/translations/%s/dialogues.tra~
END

/* remove journal entry for SoD+EET */
ACTION_IF ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~)) THEN BEGIN
/* erase journal entry about stolen Sword from Sarevok */
<<<<<<<< .../erasejournal_sod.d
ADD_TRANS_ACTION BDIMOEN BEGIN 10 END BEGIN END ~EraseJournalEntry(@10001) AddJournalEntry(@10010,QUEST_DONE)~
>>>>>>>>
COMPILE EVALUATE_BUFFER ~.../erasejournal_sod.d~
END

ACTION_IF GAME_IS ~bgt~ THEN BEGIN
  INCLUDE ~c#endlessbg1/sarevok_sword/theft_sarevoksword_bgt.tpa~
  COMPILE EVALUATE_BUFFER ~c#endlessbg1/sarevok_sword/dialogues_sarevoksword_bgt.d~ USING ~c#endlessbg1/translations/%s/dialogues.tra~
END //bgt

/* remove journal entry for BGT+EET */
ACTION_IF GAME_IS ~bgt eet~ THEN BEGIN
/* erase journal entry about stolen Sword from Sarevok and set new one */
<<<<<<<< .../erasejournal_id.baf
IF
	PartyHasItem("SW2H16")  // Sarevok's Chaor Sword
	Global("C#st_SarevoksSwordID","GLOBAL",0)
THEN
	RESPONSE #100
		EraseJournalEntry(@10000)
		EraseJournalEntry(@10008)
		EraseJournalEntry(@10010) 
		AddJournalEntry(@10011,QUEST_DONE)
		SetGlobal("C#st_SarevoksSwordID","GLOBAL",1)
END
>>>>>>>>
EXTEND_BOTTOM ~AR0602.bcs~ ~.../erasejournal_id.baf~ EVALUATE_BUFFER
END

ACTION_IF ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~)) THEN BEGIN
<<<<<<<< .../bd0103_remove_Sarevoks_sword.baf
/* Sarevok's Sword will be stolen after PC returns from Korlasz' Dungeon */
IF
  Global("C#st_HeroTransferItemsPC","MYAREA",1)
  Global("C#st_RemoveSwordPCChest","MYAREA",0)
  HasItem("c#stsrvs","PlayerChest00")
THEN
  RESPONSE #100
    ActionOverride("PlayerChest00",DestroyItem("c#stsrvs"))
    SetGlobal("C#st_RemoveSwordPCChest","MYAREA",1)
END
>>>>>>>>
EXTEND_BOTTOM ~bd0103.bcs~ ~.../bd0103_remove_Sarevoks_sword.baf~ EVALUATE_BUFFER 
END //SoD+EET

////////////////////////////////////////////////////////////////////////
//////
BEGIN @5004   /* ~Imoen and Duke Jannath (Imoen Gets Residence Inside Palace)~ */ DESIGNATED 5 
LABEL EBG1_ImoenJannath
REQUIRE_COMPONENT ~c#endlessbg1.tp2~ ~0~ @5003
FORBID_COMPONENT ~transitions/setup-transitions.tp2~ 0 @5022 /* ~This component of ebg1 is already replaced by Transitions.~ */
REQUIRE_PREDICATE (GAME_IS ~eet bgee bgt~) @5013 /* ~This component is only compatible with BG:EE, SoD, BGT, and EET.~ */
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5023
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5023

ADD_JOURNAL @10004 @10005 USING ~c#endlessbg1/translations/%s/dialogues.tra~

INCLUDE ~c#endlessbg1/imoen_jannath/imoen_jannath.tpa~

ACTION_IF GAME_IS ~bgee eet~ THEN BEGIN
// Get state for BELT %victory% (dialogues_ee.d)
/* ~<CHARNAME>, you were successful! You defeated Sarevok who conspired against the city, the Sword Coast, even Amn - for his own dark goals and purpose.~ */
OUTER_SET belt_victory = STATE_WHICH_SAYS 0 IN ~c#endlessbg1/translations/%s/dialogues.tra~ FROM ~BELT~
END

// Get state for LIIA %victory% (dialogues_ee.d)
/* ~Thank you for defeating Sarevok, <CHARNAME>!~ */
OUTER_SET liia_victory = STATE_WHICH_SAYS 10 IN ~c#endlessbg1/translations/%s/dialogues.tra~ FROM ~LIIA~

COMPILE EVALUATE_BUFFER ~c#endlessbg1/imoen_jannath/imoen_jannath.d~ USING ~c#endlessbg1/translations/%s/dialogues.tra~


////////////////////////////////////////////////////////////////////////
//////
BEGIN @5005 /* ~Duke Eltan Is in the Palace~ */ DESIGNATED 6 
LABEL EBG1_EltanInPalace
REQUIRE_COMPONENT ~c#endlessbg1.tp2~ ~0~ @5003
FORBID_COMPONENT ~transitions/setup-transitions.tp2~ 70 @5022 /* ~This component of ebg1 is already replaced by Transitions.~ */
FORBID_COMPONENT ~transitions/setup-transitions.tp2~ 71 @5022 /* ~This component of ebg1 is already replaced by Transitions.~ */
FORBID_COMPONENT ~transitions/setup-transitions.tp2~ 72 @5022 /* ~This component of ebg1 is already replaced by Transitions.~ */
REQUIRE_PREDICATE (GAME_IS ~eet bgee bgt~) @5013 /* ~This component is only compatible with BG:EE, SoD, BGT, and EET.~ */
FORBID_COMPONENT ~bg1re/bg1re.tp2~ 6 @5020 
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5023
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5023

ACTION_IF GAME_IS ~bgee eet~ THEN BEGIN
// Get state for BELT %victory% (dialogues_ee.d)
/* ~<CHARNAME>, you were successful! You defeated Sarevok who conspired against the city, the Sword Coast, even Amn - for his own dark goals and purpose.~ */
OUTER_SET belt_victory = STATE_WHICH_SAYS 0 IN ~c#endlessbg1/translations/%s/dialogues.tra~ FROM ~BELT~
END

INCLUDE ~c#endlessbg1/duke_eltan/duke_eltan.tpa~
COMPILE EVALUATE_BUFFER ~c#endlessbg1/duke_eltan/duke_eltan.d~ USING ~c#endlessbg1/translations/%s/dialogues.tra~


////////////////////////////////////////////////////////////////////////
//////
BEGIN @5006 /* ~Flaming Fist After Final Fight~ */ DESIGNATED 7 
LABEL EBG1_FFHealerInBhaalTemple
REQUIRE_COMPONENT ~c#endlessbg1.tp2~ ~0~ @5003
/* ? - is connected to Sarevok's items! */
FORBID_COMPONENT ~transitions/setup-transitions.tp2~ 20 @5022 /* ~This component of ebg1 is already replaced by Transitions.~ */
FORBID_COMPONENT ~transitions/setup-transitions.tp2~ 21 @5022 /* ~This component of ebg1 is already replaced by Transitions.~ */
REQUIRE_PREDICATE (GAME_IS ~eet bgee bgt~) @5000 /* ~This mod is only compatible with BG:EE, SoD, BGT, and EET.~ */
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5023
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5023

INCLUDE ~c#endlessbg1/ffhealer/ffhealer.tpa~
COMPILE EVALUATE_BUFFER ~c#endlessbg1/ffhealer/ffhealer.d~ USING ~c#endlessbg1/translations/%s/dialogues.tra~


///////////////////////////////////////////////////////////////////////
////// /* ~Elminster makes an Appearance~ */

BEGIN @5015 /* ~jastey's Version~ */ DESIGNATED 8 
LABEL  EBG1_ElminsterAppearance_jastey
SUBCOMPONENT @5007 /* ~Elminster makes an Appearance~ */
REQUIRE_COMPONENT ~c#endlessbg1.tp2~ ~0~ @5003
FORBID_COMPONENT ~transitions/setup-transitions.tp2~ 60 @5022 /* ~This component of ebg1 is already replaced by Transitions.~ */
REQUIRE_PREDICATE (GAME_IS ~eet bgee bgt~) @5013 /* ~This component is only compatible with BG:EE, SoD, BGT, and EET.~ */
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5023
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5023

ADD_JOURNAL @10006 USING ~c#endlessbg1/translations/%s/dialogues.tra~

INCLUDE ~c#endlessbg1/elminster/elminster.tpa~
COMPILE EVALUATE_BUFFER ~c#endlessbg1/elminster/elminster.d~ USING ~c#endlessbg1/translations/%s/dialogues.tra~

BEGIN @5016 /* ~Restored BG1 Text~ */ DESIGNATED 9 
LABEL EBG1_ElminsterAppearance_restored
SUBCOMPONENT @5007 /* ~Elminster makes an Appearance~ */
REQUIRE_COMPONENT ~c#endlessbg1.tp2~ ~0~ @5003
FORBID_COMPONENT ~transitions/setup-transitions.tp2~ 60 @5022 /* ~This component of ebg1 is already replaced by Transitions.~ */
REQUIRE_PREDICATE (GAME_IS ~eet bgee bgt~) @5013 /* ~This component is only compatible with BG:EE, SoD, BGT, and EET.~ */

INCLUDE ~c#endlessbg1/elminster/elminster.tpa~
COMPILE EVALUATE_BUFFER ~c#endlessbg1/elminster/elminster_bg.d~ USING ~c#endlessbg1/translations/%s/dialogues.tra~


////////////////////////////////////////////////////////////////////////
//////
BEGIN @5009 /* ~First Refugees Come to Baldur's Gate~ */ DESIGNATED 10 
LABEL EBG1_RefugeesInBGCity
REQUIRE_COMPONENT ~c#endlessbg1.tp2~ ~0~ @5003
FORBID_COMPONENT ~transitions/setup-transitions.tp2~ 80 @5022 /* ~This component of ebg1 is already replaced by Transitions.~ */
REQUIRE_PREDICATE ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~)) @5012 /* ~This mod is only compatible with SoD and EET.~ */
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5023
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5023

ADD_JOURNAL @10007 USING ~c#endlessbg1/translations/%s/dialogues.tra~

INCLUDE ~c#endlessbg1/refugees_sod/refugees_sod.tpa~
COMPILE EVALUATE_BUFFER ~c#endlessbg1/refugees_sod/refugees_sod.d~ USING ~c#endlessbg1/translations/%s/dialogues.tra~


////////////////////////////////////////////////////////////////////////
//////
BEGIN @5010 /* ~Ophyllis the Treasurer is Inside Palace Dungeon~ */ DESIGNATED 11 
LABEL EBG1_TreasurerInPalaceDungeon
REQUIRE_COMPONENT ~c#endlessbg1.tp2~ ~0~ @5003
REQUIRE_PREDICATE ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~)) @5012 /* ~This mod is only compatible with SoD and EET.~ */
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5023
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5023

INCLUDE ~c#endlessbg1/ophyllis_sod/ophyllis_sod.tpa~
COMPILE EVALUATE_BUFFER ~c#endlessbg1/ophyllis_sod/ophyllis_sod.d~ USING ~c#endlessbg1/translations/%s/dialogues.tra~


////////////////////////////////////////////////////////////////////////
//////
BEGIN @5011 /* ~Denkod in Thieves' Guild Comments on Sarevok's Death~ */ DESIGNATED 12 
LABEL EBG1_DenkodCommentsOnSarevoksDeath
REQUIRE_COMPONENT ~c#endlessbg1.tp2~ ~0~ @5003
FORBID_COMPONENT ~transitions/setup-transitions.tp2~ 50 @5022 /* ~This component of ebg1 is already replaced by Transitions.~ */
REQUIRE_PREDICATE (GAME_IS ~eet bgee bgt~) @5013 /* ~This component is only compatible with BG:EE, SoD, BGT, and EET.~ */
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5023
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5023

COMPILE EVALUATE_BUFFER ~c#endlessbg1/denkod_add/denkod.d~ USING ~c#endlessbg1/translations/%s/dialogues.tra~

ACTION_IF GAME_IS ~bgee eet~ THEN BEGIN
  INCLUDE ~c#endlessbg1/denkod_add/denkod_ee.tpa~
END

ACTION_IF GAME_IS ~bgt~ THEN BEGIN
  INCLUDE ~c#endlessbg1/denkod_add/denkod_bgt.tpa~
END //bgt



////////////////////////////////////////////////////////////////////////
//////
BEGIN @5017 /* ~Skip Thieves' Maze Once After Sarevok's Death~ */ DESIGNATED 13 LABEL EBG1_SkipThievesMazeOnce
REQUIRE_COMPONENT ~c#endlessbg1.tp2~ ~0~ @5003
REQUIRE_PREDICATE (GAME_IS ~eet bgee bgt~) @5013 /* ~This component is only compatible with BG:EE, SoD, BGT, and EET.~ */
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5023
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5023

ACTION_IF GAME_IS ~bgee eet~ THEN BEGIN
  OUTER_SPRINT ~exit7246~ ~exit0146~
  OUTER_SPRINT ~Door7246~ ~Door0146~
END

ACTION_IF GAME_IS ~bgt~ THEN BEGIN
  OUTER_SPRINT ~exit7246~ ~exit7246~
  OUTER_SPRINT ~Door7246~ ~Door7246~
END

/* add travel trigger directly into Thieves Guild */
COPY_EXISTING ~%Undercity%.are~ ~override~
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 2    //travel
    fj_box_left     = 5088  //kleinstes x
    fj_box_top      = 787  //kleinstes y
    fj_box_right    = 5118  //größtes x
    fj_box_bottom   = 1130   //größtes y
    fj_cursor_idx   = 34   //door
    fj_vertex_0     = 5088 + (787 << 16)   //kleinstes x +  kleinstes y
    fj_vertex_1     = 5088 + (1130 << 16)   //größtes x + kleinstes y
    fj_vertex_2     = 5118 + (1130 << 16)   //größtes x + größtes y
    fj_vertex_3     = 5118 + (787 << 16)   //kleinstes x + größtes y
    fj_flags        = 0b0000000000000100  //full party required, autosave
    STR_VAR
    fj_structure_type   = region
    fj_name             = c#ebg101
    fj_destination_area = EVAL ~%EBaldursGate_ThievesGuild%~
    fj_destination_name = EVAL ~%exit7246%~
  END

/* Sarevok dead: deactivate Trigger Door7246 in Undercity once */

<<<<<<<< .../ar0123_12_add.baf
IF
	Global("C#st_SarevokDeadSkipMaze","GLOBAL",0)
THEN
	RESPONSE #100
		TriggerActivation("c#ebg101",FALSE)
		SetGlobal("C#st_SarevokDeadSkipMaze","GLOBAL",1)
END

IF
	Dead("Sarevok")
	GlobalLT("C#st_SarevokDeadSkipMaze","GLOBAL",2)
THEN
	RESPONSE #100
		TriggerActivation("%Door7246%",FALSE)
		TriggerActivation("c#ebg101",TRUE)
		SetGlobal("C#st_SarevokDeadSkipMaze","GLOBAL",2)
END

/* set back normal exit after PC was in Thieves Guild */
IF
	Dead("Sarevok")
	Global("C#st_SarevokDeadSkipMaze","GLOBAL",3)
THEN
	RESPONSE #100
		TriggerActivation("c#ebg101",FALSE)
		TriggerActivation("%Door7246%",TRUE)
		SetGlobal("C#st_SarevokDeadSkipMaze","GLOBAL",4)
END
>>>>>>>>
EXTEND_BOTTOM ~%Undercity%.bcs~ ~.../ar0123_12_add.baf~ EVALUATE_BUFFER

<<<<<<<< .../ar0146_12_add.baf
IF
	InMyArea(Player1)
	Global("C#st_SarevokDeadSkipMaze","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("C#st_SarevokDeadSkipMaze","GLOBAL",3)
END
>>>>>>>>
EXTEND_BOTTOM ~%EBaldursGate_ThievesGuild%.bcs~ ~.../ar0146_12_add.baf~ EVALUATE_BUFFER  

////////////////////////////////////////////////////////////////////////
/* Korlasz' Dungeon is in BG1 */
////////////////////////////////////////////////////////////////////////
BEGIN @5024 DESIGNATED 14 
LABEL EBG1_KorlaszDungeonBG1
REQUIRE_COMPONENT ~c#endlessbg1.tp2~ ~0~ @5003
REQUIRE_PREDICATE (GAME_INCLUDES ~sod~) @5012
FORBID_COMPONENT ~imoen_forever.tp2~ ~10~ @5025 /* Imoen4Ever's "Imoen remains in the group in Korlasz' dungeon" needs to be deinstalled for this.~ */
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5023
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5023


/* define new campaign name to SoD will lead to bd0103 */
ACTION_IF GAME_IS ~bgee~ BEGIN
APPEND ~CAMPAIGN.2DA~ ~
C#STSD         BDBALDUR    -1      1          BDBANTER       *              SODMAREA       74       -1        NPCLVLDS      *          BDDIALOG       sodsave          C#EBG1ST       STRTGOLD       *           SODWEAP      BDSTWEAP       *              *             BGMAP          BDBALDUR       1              0xFFFFFFFF     1          SODCIN01           PARTY          SODINTER       SODYEARS      SODREPUTATI      SODCLTXT       SODRACE~
END
ACTION_IF GAME_IS ~eet~ BEGIN
/* EET */
APPEND ~CAMPAIGN.2DA~ ~
C#STSD         BDBALDUR    -1      1          BDBANTER       *              MASTAREA       152      -1        *          BDPARTY        BDDIALOG       save             C#EBG1ST       STRTGOLD       *         SODWEAP      BDSTWEAP       *              *              BGMAP          BDBALDUR       1              0xFFFFFFFF     1          SODCIN01           PARTY          SODINTER       SODYEARS        REPUTATI      SODCLTXT       SODRACE~
END

/* change starting area */
COPY ~c#endlessbg1/korlasz_dungeon_bg1/c#ebg1st.2da~ ~override~

/* change the campaign name in bdsodtrn.bcs so we land in bd01013.are */

COPY_EXISTING ~bdsodtrn.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(CopyGroundPilesTo("BD0120",\[949\.1686\])\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(MoveToCampaign("SoD")\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~MoveToCampaign("C#STSD")~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY


/* Add Entrance to Korlasz Dungeon to BG city: ar0700.are */
COPY_EXISTING ~%CentralBaldursGate%.are~ ~override~
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 2    //travel
    fj_box_left     = 1005  //kleinstes x
    fj_box_top      = 3172  //kleinstes y
    fj_box_right    = 1150  //größtes x
    fj_box_bottom   = 3300   //größtes y
    fj_cursor_idx   = 28 //stairs
    fj_vertex_0     = 1005 + (3300 << 16)   //kleinstes x +  kleinstes y
    fj_vertex_1     = 1150 + (3299 << 16)   //größtes x + kleinstes y
    fj_vertex_2     = 1142 + (3172 << 16)   //größtes x + größtes y
    fj_vertex_3     = 1017 + (3176 << 16)   //kleinstes x + größtes y
    STR_VAR
    fj_structure_type   = region
    fj_name             = C#EBG1_TranBD0120
    fj_destination_area = EVAL ~bd0120~
    fj_destination_name = EVAL ~C#EBG1_Exit0700~ 
  END
/* add info trigger for closed entrance */
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 1    //trigger
    fj_box_left     = 1005  //kleinstes x
    fj_box_top      = 3172  //kleinstes y
    fj_box_right    = 1150  //größtes x
    fj_box_bottom   = 3300   //größtes y
    fj_cursor_idx   = 22 //question mark
    fj_vertex_0     = 1005 + (3300 << 16)   //kleinstes x +  kleinstes y
    fj_vertex_1     = 1150 + (3299 << 16)   //größtes x + kleinstes y
    fj_vertex_2     = 1142 + (3172 << 16)   //größtes x + größtes y
    fj_vertex_3     = 1017 + (3176 << 16)   //kleinstes x + größtes y
    fj_info_point_strref = RESOLVE_STR_REF (@24)
    STR_VAR
    fj_structure_type   = region
    fj_name             = c#ebg1korlasz
//    fj_reg_script   = c#ebg1ko
  END
BUT_ONLY

/* Add Entrance to Korlasz Dungeon to BG Docks: ar1200.are */
COPY_EXISTING ~%BaldursGateDocks%.are~ ~override~
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 2    //travel
    fj_box_left     = 1019  //kleinstes x
    fj_box_top      = 74  //kleinstes y
    fj_box_right    = 1154  //größtes x
    fj_box_bottom   = 189   //größtes y
    fj_cursor_idx   = 28 //stairs
    fj_vertex_0     = 1030 + (186 << 16)   //kleinstes x +  kleinstes y
    fj_vertex_1     = 1154 + (189 << 16)   //größtes x + kleinstes y
    fj_vertex_2     = 1152 + (78 << 16)   //größtes x + größtes y
    fj_vertex_3     = 1019 + (74 << 16)   //kleinstes x + größtes y
    STR_VAR
    fj_structure_type   = region
    fj_name             = C#EBG1_TranBD0120
    fj_destination_area = EVAL ~bd0120~
    fj_destination_name = EVAL ~C#EBG1_Exit0700~ 
  END
/* add info trigger for closed entrance */
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 1    //trigger
    fj_box_left     = 1019  //kleinstes x
    fj_box_top      = 74  //kleinstes y
    fj_box_right    = 1154  //größtes x
    fj_box_bottom   = 189   //größtes y
    fj_cursor_idx   = 22 //question mark
    fj_vertex_0     = 1030 + (186 << 16)   //kleinstes x +  kleinstes y
    fj_vertex_1     = 1154 + (189 << 16)   //größtes x + kleinstes y
    fj_vertex_2     = 1152 + (78 << 16)   //größtes x + größtes y
    fj_vertex_3     = 1019 + (74 << 16)   //kleinstes x + größtes y
    fj_info_point_strref = RESOLVE_STR_REF (@24)
    STR_VAR
    fj_structure_type   = region
    fj_name             = c#ebg1korlasz
//    fj_reg_script   = c#ebg1ko
  END


/* Add Entrance to Korlasz Dungeon to BG city: bd0050.are */
COPY_EXISTING ~bd0050.are~ ~override~
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 2    //travel
    fj_box_left     = 1110  //kleinstes x
    fj_box_top      = 141  //kleinstes y
    fj_box_right    = 1217  //größtes x
    fj_box_bottom   = 245   //größtes y
    fj_cursor_idx   = 28 //stairs
    fj_vertex_0     = 1119 + (245 << 16)   //kleinstes x +  kleinstes y
    fj_vertex_1     = 1217 + (244 << 16)   //größtes x + kleinstes y
    fj_vertex_2     = 1214 + (145 << 16)   //größtes x + größtes y
    fj_vertex_3     = 1110 + (141 << 16)   //kleinstes x + größtes y
    STR_VAR
    fj_structure_type   = region
    fj_name             = C#EBG1_TranBD0120
    fj_destination_area = EVAL ~bd0120~
    fj_destination_name = EVAL ~C#EBG1_Exit0700~ 
  END
/* add info trigger for closed entrance */
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 1    //trigger
    fj_box_left     = 1110  //kleinstes x
    fj_box_top      = 141  //kleinstes y
    fj_box_right    = 1217  //größtes x
    fj_box_bottom   = 245   //größtes y
    fj_cursor_idx   = 22 //question mark
    fj_vertex_0     = 1119 + (245 << 16)   //kleinstes x +  kleinstes y
    fj_vertex_1     = 1217 + (244 << 16)   //größtes x + kleinstes y
    fj_vertex_2     = 1214 + (145 << 16)   //größtes x + größtes y
    fj_vertex_3     = 1110 + (141 << 16)   //kleinstes x + größtes y
    fj_info_point_strref = RESOLVE_STR_REF (@24)
    STR_VAR
    fj_structure_type   = region
    fj_name             = c#ebg1korlasz
//    fj_reg_script   = c#ebg1ko
  END
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_loc_x       = 1232    //x
    fj_loc_y       = 108   //y
    fj_orientation = 14
    STR_VAR
    fj_structure_type = entrance
    fj_name           = C#EBG1_Exit0120
  END
BUT_ONLY

/* Korlasz crypt bd0120 */
/* patch bdrope.bcs so it doesn't initiate Imoen's dialogue */
COPY_EXISTING ~bdrope.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
/* cases for Imoen in party */
		SPRINT textToReplace ~\(See("imoen2?")\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY CASE_INSENSITIVE ~%textToReplace%~ ~\1 Global("C#EBG1_KorlaszQuest","GLOBAL",0)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

<<<<<<<< .../bdrope-et.baf
/* patch bdrope.bcs so it teleports the group out */
IF
	Clicked([ANYONE])
	NearLocation(LastTrigger,790,1660,12)
	Global("C#EBG1_ChangeRopeTrigger","MYAREA",2) 
THEN
	RESPONSE #100
		ClearAllActions()
		StartCutSceneMode()
		StartCutSceneEx("c#st2bc",TRUE)
END
>>>>>>>>
EXTEND_TOP ~bdrope.bcs~ ~.../bdrope-et.baf~ EVALUATE_BUFFER

/* cutscene out of Korlasz dungeon in bdrope.bcs */
COMPILE ~c#endlessbg1/korlasz_dungeon_bg1/c#st2bc.baf~ EVALUATE_BUFFER

/* Add Travel points and Entrance point to Koralsz' Crypt bd0120.are */
COPY_EXISTING ~bd0120.are~ ~override~
/* travel region to SoD-area bd0050*/
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 2    //travel
    fj_box_left     = 750  //kleinstes x
    fj_box_top      = 1528  //kleinstes y
    fj_box_right    = 810  //größtes x
    fj_box_bottom   = 1683   //größtes y
    fj_cursor_idx   = 28 //stairs
    fj_vertex_0     = 756 + (1683 << 16)   //kleinstes x +  kleinstes y
    fj_vertex_1     = 810 + (1681 << 16)   //größtes x + kleinstes y
    fj_vertex_2     = 804 + (1532 << 16)   //größtes x + größtes y
    fj_vertex_3     = 750 + (1528 << 16)   //kleinstes x + größtes y
    STR_VAR
    fj_structure_type   = region
    fj_name             = C#EBG1_TranBD0050
    fj_destination_area = EVAL ~bd0050~
    fj_destination_name = EVAL ~C#EBG1_Exit0120~ 
  END
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_loc_x       = 920    //x
    fj_loc_y       = 1601   //y
    fj_orientation = 13
    STR_VAR
    fj_structure_type = entrance
    fj_name           = C#EBG1_Exit0700
  END
BUT_ONLY


/* cutscene into Korlasz dungeon in Belt's dialogue */
COMPILE ~c#endlessbg1/korlasz_dungeon_bg1/c#st2kd.baf~ EVALUATE_BUFFER


/* add transition to Korlasz dungeon to Belt's dialogue */
// Get state for BELT %belt_ebg1_8% (in dialogues_ee.d)
/* @8 /* ~I see. Still, the city is in your debt. Fare well, Hero of Baldur's Gate.~ */
*/
OUTER_SET belt_ebg1_8 = STATE_WHICH_SAYS 8 IN ~c#endlessbg1/translations/%s/dialogues.tra~ FROM ~BELT~
// Get state for BELT %belt_ebg1_13% (in dialogues_ee.d)
/* @13 /* ~The city is in your debt, Hero of Baldur's Gate. */
*/
OUTER_SET belt_ebg1_13 = STATE_WHICH_SAYS 13 IN ~c#endlessbg1/translations/%s/dialogues.tra~ FROM ~BELT~

// Get state for BELT %belt_ebg1_14% (in dialogues_ee.d)
/* @14 /* Please follow me, the Flaming Fist soldiers will guide you to the hide-out of Sarevok's last follower.~ */
*/
OUTER_SET belt_ebg1_14 = STATE_WHICH_SAYS 14 IN ~c#endlessbg1/translations/%s/dialogues.tra~ FROM ~BELT~

// Get state for LIIA %victory% (dialogues_ee.d)
/* ~Thank you for defeating Sarevok, <CHARNAME>!~ */
OUTER_SET liia_victory = STATE_WHICH_SAYS 10 IN ~c#endlessbg1/translations/%s/dialogues.tra~ FROM ~LIIA~

COMPILE EVALUATE_BUFFER ~c#endlessbg1/korlasz_dungeon_bg1/korlasz_dungeon_bg1.d~ USING ~c#endlessbg1/translations/%s/dialogues.tra~


/* Inside Korlasz' Dungeon: disable all instances that make no sense for Imoen in party, but do it non-destructively. Thank you argent77 for the code! */
INCLUDE ~c#endlessbg1/korlasz_dungeon_bg1/add_trans_trigger_imoen_in_kd_ebg1.tpa~

/* change dialogue of the FF soldiers at crypt exit, and prevent them to transition to bdimoen: BDFF1000.dlg */
/* #69736 /* ~Thank you. I'm ready to return to the Ducal Palace now.~ */ */
// List of all potential response strrefs to check
ACTION_DEFINE_ASSOCIATIVE_ARRAY response_strrefs BEGIN
  ~%eet_2%69736~ => 1
END
  
COPY_EXISTING ~BDFF1000.dlg~ ~override~
  // scanning listed dialog states
  PATCH_FOR_EACH state IN 0 BEGIN
    LPF GET_RESPONSE_STRREFS INT_VAR state RET strrefs RET_ARRAY strrefs END
    TEXT_SPRINT indices ~~  // a temporary variable for building the index list
    FOR (i = 0; i < strrefs; ++i) BEGIN
      SET value = $strrefs(~%i%~)
      // Include index only if strref is listed in the response_strrefs array
      PATCH_IF (VARIABLE_IS_SET $response_strrefs(~%value%~)) BEGIN
        TEXT_SPRINT indices ~%indices% %i%~ // building list of indices
      END
    END
    // prevent adding a trigger if index list is empty
    PATCH_IF (~%indices%~ STR_EQ ~~) BEGIN
      TEXT_SPRINT indices %IF ~False()~%
    END
    // initialize variables responses_39, responses_40, ...
    // EVAL is used to create the variable name dynamically
    TEXT_SPRINT EVAL ~responses_BDFF1000_%state%~ ~%indices%~
    PATCH_PRINT ~Variable %state%: %indices%~
  END
BUT_ONLY

/* change dialogue of the FF soldiers at crypt exit, and prevent them to transition to bdimoen: BDFF1001.dlg */
/* #69732 /* ~I'm ready to return to the Ducal Palace now and report our success.~ */ */
// List of all potential response strrefs to check
ACTION_DEFINE_ASSOCIATIVE_ARRAY response_strrefs BEGIN
  ~%eet_2%69732~ => 1
END
  
COPY_EXISTING ~BDFF1001.dlg~ ~override~
  // scanning listed dialog states
  PATCH_FOR_EACH state IN 0 BEGIN
    LPF GET_RESPONSE_STRREFS INT_VAR state RET strrefs RET_ARRAY strrefs END
    TEXT_SPRINT indices ~~  // a temporary variable for building the index list
    FOR (i = 0; i < strrefs; ++i) BEGIN
      SET value = $strrefs(~%i%~)
      // Include index only if strref is listed in the response_strrefs array
      PATCH_IF (VARIABLE_IS_SET $response_strrefs(~%value%~)) BEGIN
        TEXT_SPRINT indices ~%indices% %i%~ // building list of indices
      END
    END
    // prevent adding a trigger if index list is empty
    PATCH_IF (~%indices%~ STR_EQ ~~) BEGIN
      TEXT_SPRINT indices %IF ~False()~%
    END
    // initialize variables responses_39, responses_40, ...
    // EVAL is used to create the variable name dynamically
    TEXT_SPRINT EVAL ~responses_BDFF1001_%state%~ ~%indices%~
    PATCH_PRINT ~Variable %state%: %indices%~
  END
BUT_ONLY

/* change dialogue of the FF soldiers at crypt exit, and prevent them to transition to bdimoen: BDFF1002.dlg */
/* #69732 /* ~I'm ready to return to the Ducal Palace now and report our success.~ */ */
// List of all potential response strrefs to check
ACTION_DEFINE_ASSOCIATIVE_ARRAY response_strrefs BEGIN
  ~%eet_2%69732~ => 1
END
  
COPY_EXISTING ~BDFF1002.dlg~ ~override~
  // scanning listed dialog states
  PATCH_FOR_EACH state IN 0 BEGIN
    LPF GET_RESPONSE_STRREFS INT_VAR state RET strrefs RET_ARRAY strrefs END
    TEXT_SPRINT indices ~~  // a temporary variable for building the index list
    FOR (i = 0; i < strrefs; ++i) BEGIN
      SET value = $strrefs(~%i%~)
      // Include index only if strref is listed in the response_strrefs array
      PATCH_IF (VARIABLE_IS_SET $response_strrefs(~%value%~)) BEGIN
        TEXT_SPRINT indices ~%indices% %i%~ // building list of indices
      END
    END
    // prevent adding a trigger if index list is empty
    PATCH_IF (~%indices%~ STR_EQ ~~) BEGIN
      TEXT_SPRINT indices %IF ~False()~%
    END
    // initialize variables responses_39, responses_40, ...
    // EVAL is used to create the variable name dynamically
    TEXT_SPRINT EVAL ~responses_BDFF1002_%state%~ ~%indices%~
    PATCH_PRINT ~Variable %state%: %indices%~
  END
BUT_ONLY

COMPILE ~c#endlessbg1/korlasz_dungeon_bg1/Imoen_in_group_kd_ebg1.d~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bdimoen.bcs~ ~c#endlessbg1/korlasz_dungeon_bg1/c#ebg1im.baf~ EVALUATE_BUFFER

//bd0120.BCS
COPY_EXISTING ~bd0120.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
/* cases for Imoen in party */
		SPRINT textToReplace ~\(InPartyAllowDead("imoen2?")\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY CASE_INSENSITIVE ~%textToReplace%~ ~\1 Global("C#st_ImoenInGroupKD","GLOBAL",0)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(!InMyArea("imoen2?")\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 !Global("C#st_ImoenInGroupKD","GLOBAL",1)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(ActionOverride("imoen2?",JumpToPoint(\[1770\.1520\]))[%newline%]*ActionOverride("imoen2?",Face(N))\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("C#EBG1_ImoenMoveInCrypt","GLOBAL",1)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(ActionOverride("imoen2?",JumpToPoint(\[1545\.700\]))[%newline%]*ActionOverride("imoen2?",Face(NE))\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("C#EBG1_ImoenMoveInCrypt","GLOBAL",2)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
/* Other patch stuff to prevent to do SoD scriptings */
		SPRINT textToReplace ~\(Global("BD0120_START","GLOBAL",0)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~False()~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

ACTION_IF GAME_IS ~eet~ BEGIN
/* more patching if it's EET */
COPY_EXISTING ~bd0120.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(Global("K#NewGame","BD0120",0)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 !Global("SOD_fromimport","global",2)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY
END


EXTEND_TOP ~bd0120.BCS~ ~c#endlessbg1/korlasz_dungeon_bg1/c#ebg1_kd_bg1_bd0120.baf~ EVALUATE_BUFFER

<<<<<<<< .../bd0120-et.baf

/* deactivate travel triggers */
IF
	Global("C#EBG1_ChangeRopeTrigger","MYAREA",0)
THEN
	RESPONSE #100
		TriggerActivation("C#EBG1_TranBD0050",FALSE)
		SetGlobal("C#EBG1_ChangeRopeTrigger","MYAREA",1)
END

IF
	GlobalGT("C#EBG1_KorlaszQuest","GLOBAL",2)
	GlobalLT("C#EBG1_KorlaszQuest","GLOBAL",99)
	Global("C#EBG1_ClearCresKD","MYAREA",0)
THEN
	RESPONSE #100
		ActionOverride("BDFF1000",DestroySelf())
		ActionOverride("BDFF1001",DestroySelf())
		ActionOverride("BDFF1002",DestroySelf())
		ActionOverride("BDKORLAS",DestroySelf())
		SetGlobal("C#EBG1_ClearCresKD","MYAREA",1)
END

IF
	InPartyAllowDead("%IMOEN_DV%")
	Global("C#st_ImoenInGroupKD","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#st_ImoenInGroupKD","GLOBAL",1)
		ApplySpellRES("bdresurr","%IMOEN_DV%")  
		SmallWait(1)
	        ApplySpellRES("bdrejuve","%IMOEN_DV%")  
	  	SmallWait(1)
END

IF
	Global("bd_plot","global",2) 
	InMyArea("%IMOEN_DV%")
	!Dead("%IMOEN_DV%")
	!StateCheck("%IMOEN_DV%",CD_STATE_NOTVALID)
THEN
	RESPONSE #100
		ActionOverride("%IMOEN_DV%",StartDialogOverride("bdimoen",Player1))
END

IF
	Global("bd_plot","global",2) 
	OR(3) !InMyArea("%IMOEN_DV%") Dead("%IMOEN_DV%")
		StateCheck("%IMOEN_DV%",CD_STATE_NOTVALID)
THEN
	RESPONSE #100
		SetGlobal("bd_plot","global",10)
		SetGlobal("BDSH_Imoen_Floor","GLOBAL",1)
END

IF
	GlobalLT("bd_plot","global",40)
	OR(2)
		Dead("bdkorlas")
		Global("BD_KORLASZ_SURRENDER","GLOBAL",1)
	!InMyArea("%IMOEN_DV%")
	InMyArea(Player1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("bd_plot","global",40)
		SetGlobal("bd_Imoen_farewell","bd0120",2)
		SetGlobal("C#EBG1_KorlaszQuest","GLOBAL",2)
		AddJournalEntry(%eet_2%69028,QUEST_DONE)
		SetInterrupt(TRUE)
END

/* change rope trigger so player can leave the dungeon */

/* exit to AR0700.are */
IF
	GlobalGT("C#EBG1_KorlaszQuest","GLOBAL",0)
	GlobalGT("bd_plot","global",2)
	GlobalLT("bd_plot","global",50)
	GlobalLT("C#EBG1_ChangeRopeTrigger","MYAREA",2)
THEN
	RESPONSE #100
		DisplayStringHead("C#EBG1_TranBD0050",%eet_2%59576)  // A sturdy rope hanging down into the tomb offers the only viable exit from the house of the dead.
		SetGlobal("C#EBG1_ChangeRopeTrigger","MYAREA",2)
END

/* exit to bd0050.are */
IF
	GlobalGT("C#EBG1_KorlaszQuest","GLOBAL",0)
	GlobalGT("bd_plot","global",49)
	GlobalLT("C#EBG1_ChangeRopeTrigger","MYAREA",3)
THEN
	RESPONSE #100
		TriggerActivation("Rope",FALSE)
		TriggerActivation("C#EBG1_TranBD0050",TRUE)
		SetGlobal("C#EBG1_ChangeRopeTrigger","MYAREA",3)
END

/* exit to bd0700.are in EET, assuming it's the BG1 city areas that's reinstalled in BGII */
IF
	GlobalGT("C#EBG1_KorlaszQuest","GLOBAL",0)
	GlobalGT("bd_plot","global",670)
	GlobalLT("C#EBG1_ChangeRopeTrigger","MYAREA",4)
THEN
	RESPONSE #100
		TriggerActivation("C#EBG1_TranBD0050",FALSE)
		TriggerActivation("Rope",TRUE)
		SetGlobal("C#EBG1_ChangeRopeTrigger","MYAREA",4)
END
>>>>>>>>

EXTEND_TOP ~bd0120.BCS~ ~.../bd0120-et.baf~ EVALUATE_BUFFER


/* Korlasz should no longer be in Korlasz crypt if it's done */
<<<<<<<< .../bdshkorl-et.baf
IF
	GlobalGT("C#EBG1_KorlaszQuest","GLOBAL",2)
	GlobalLT("C#EBG1_KorlaszQuest","GLOBAL",99)
	Global("BD_KORLASZ_SURRENDER","GLOBAL",1)
	AreaCheck("bd0120")	
	Global("C#EBG1_ClearKorlaszKD","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("C#EBG1_ClearKorlaszKD","MYAREA",1)
		DestroySelf()
END
>>>>>>>>
EXTEND_TOP ~bdshkorl.BCS~ ~.../bdshkorl-et.baf~ EVALUATE_BUFFER

/* Imoen should no longer be in Korlasz crypt if it's done */
<<<<<<<< .../bdshimoe-et.baf
IF
	GlobalGT("C#EBG1_KorlaszQuest","GLOBAL",2)
	GlobalLT("C#EBG1_KorlaszQuest","GLOBAL",99)
	GlobalGT("C#EBG1_ImoenIsInKD","GLOBAL",0)
	AreaCheck("bd0120")	
	Global("C#EBG1_ClearImoenKD","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("C#EBG1_ClearImoenKD","MYAREA",1)
		DestroySelf()
END
>>>>>>>>
EXTEND_TOP ~bdshimoe.BCS~ ~.../bdshimoe-et.baf~ EVALUATE_BUFFER


//bdintro.BCS
COPY_EXISTING ~bdintro.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(CreateCreature("bdshimoe",\[881\.1652\],NE)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(ActionOverride("IMOEN2?",StartDialogNoSet(Player1))\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(SetGlobal("bd_npc_camp_chapter","global",0)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(SetGlobal("sprite_is_deadsafana","GLOBAL",0)[%newline%]*SetGlobal("sprite_is_deaddynaheir","GLOBAL",0)[%newline%]*SetGlobal("sprite_is_deadminsc","GLOBAL",0)[%newline%]*SetGlobal("sprite_is_deadrasaad","GLOBAL",0)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(ChangeAIScript("bdplayer",OVERRIDE)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY


<<<<<<<< .../bdintro-et.baf
IF
	Global("C#st_ImoenInGroupKD","GLOBAL",0) 
	Global("C#EBG1_ImoenIsInKD","GLOBAL",0) 
THEN
	RESPONSE #100
		CutSceneId(Player1)
		SetGlobal("C#EBG1_ImoenIsInKD","GLOBAL",1)
		CreateCreature("bdshimoe",[881.1652],NE)
		Continue()
END

IF
	Global("SoD_fromimport","global",0)
	Global("C#EBG1_ThingsForNewGame","MYAREA",0) 

THEN
	RESPONSE #100
		SetGlobal("bd_npc_camp_chapter","global",0)
		ChangeAIScript("bdplayer",OVERRIDE)
		SetGlobal("sprite_is_deadsafana","GLOBAL",0)
		SetGlobal("sprite_is_deaddynaheir","GLOBAL",0)
		SetGlobal("sprite_is_deadminsc","GLOBAL",0)
		SetGlobal("sprite_is_deadrasaad","GLOBAL",0)
		SetGlobal("C#EBG1_ThingsForNewGame","MYAREA",1)
		Continue()
END
>>>>>>>>

EXTEND_TOP ~bdintro.BCS~ ~.../bdintro-et.baf~ EVALUATE_BUFFER


//cutskip.bcs
COPY_EXISTING ~cutskip.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(OpenDoor("door07")[%newline%]*SmallWait(11)[%newline%]*FadeFromColor(\[10\.0\],0)[%newline%]*ActionOverride("IMOEN2?",StartDialogNoSet(Player1))\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~
		OpenDoor("door07")
		SmallWait(11)
		FadeFromColor([10.0],0)
		EndCutSceneMode()~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

//bdshcut0.BCS
COPY_EXISTING ~bdshcut0.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(Global("BDSH_Spawn_Imoen","GLOBAL",0)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 Global("C#st_ImoenInGroupKD","GLOBAL",0)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY


//bd0130.BCS
COPY_EXISTING ~bd0130.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(ActionOverride("imoen2?",JumpToPoint(\[1955\.1075\]))[%newline%]*ActionOverride("imoen2?",Face(S))\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("C#EBG1_ImoenMoveInCrypt","GLOBAL",3)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(ActionOverride("imoen2?",JumpToPoint(\[3015\.1890\]))[%newline%]*ActionOverride("imoen2?",Face(SE))\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("C#EBG1_ImoenMoveInCrypt","GLOBAL",4)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(ActionOverride("imoen2?",JumpToPoint(\[1830\.2390\]))[%newline%]*ActionOverride("imoen2?",Face(SE))\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("C#EBG1_ImoenMoveInCrypt","GLOBAL",5)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(ActionOverride("imoen2?",JumpToPoint(\[970\.1435\]))[%newline%]*ActionOverride("imoen2?",Face(N))\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("C#EBG1_ImoenMoveInCrypt","GLOBAL",6)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(ActionOverride("imoen2?",JumpToPoint(\[1000\.1060\]))[%newline%]*ActionOverride("imoen2?",Face(N))\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("C#EBG1_ImoenMoveInCrypt","GLOBAL",7)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

EXTEND_TOP ~bd0130.BCS~ ~c#endlessbg1/korlasz_dungeon_bg1/c#ebg1_imo_bd0130.baf~ EVALUATE_BUFFER


/* the game expects the original NPCs to speak with their SoD dlg inside Korlasz' Dungeon so we change them (and also the scripts to prevent BG1 dialogues to fire while the group is inside) back and forth. - while in BG1 only: script checks for "Global("SOD_fromimport","global",2)" (which will be set to "1" upon transition to real SoD campaign and to "0" for direct BGII transition after Korlasz Tomb is done)  */
/* change to SoD dialogue and scripts */
/* add the first change to bdintro.bcs so beginnings cutscene in bd0120.are does not hickup */
EXTEND_TOP ~bdintro.bcs~ ~c#endlessbg1/korlasz_dungeon_bg1/switch_dlgs/switch_npc_to_sod_cutscene.baf~ EVALUATE_BUFFER 
/* script for invisible helper cre */
<<<<<<<< .../c#ebg1hc.baf

/* remove in case it's a leftover */
IF
	OR(3)
		!Global("SOD_fromimport","global",2)
		GlobalGT("bd_plot","global",40) //SoD started
		GlobalGT("chapter","GLOBAL",11) //BGII started
THEN
	RESPONSE #100		
		SetGlobal("C#EBG1_spawn_helper","MYAREA",0)
		DestroySelf()
END
IF
	OnCreation()
	InMyArea(Player1)
	Global("SOD_fromimport","global",2)
THEN
	RESPONSE #100		
		SetInterrupt(FALSE)
		SetGlobal("C#EBG1_imoen_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_dynaheir_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_minsc_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Rasaad_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Viconia_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Safana_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Edwin_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Jaheira_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Khalid_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Dorn_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Neera_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Baeloth_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Ajantis_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Alora_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Branwen_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Coran_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Eldoth_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Faldorn_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Garrick_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Kagain_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Kivan_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Montaron_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Quayle_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_SharTeel_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Skie_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Tiax_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Xan_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Xzar_to_sod","MYAREA",1)
		SetGlobal("C#EBG1_Yeslick_to_sod","MYAREA",1)
		SetInterrupt(TRUE)
END
>>>>>>>>
COMPILE ~.../c#ebg1hc.baf~ EVALUATE_BUFFER
/* invisible helper cre */
COPY ~c#endlessbg1/korlasz_dungeon_bg1/switch_dlgs/c#ebg1hc.cre~ ~override/c#ebg1hc.cre~ //
   SAY NAME1 ~~ 
   SAY NAME2 ~~ 
WRITE_EVALUATED_ASCII 0x280 ~c#ebg1hc~ #8   // Death variable
WRITE_EVALUATED_ASCII 0x2cc ~c#ebg1hc~ #8   // dialogue
WRITE_EVALUATED_ASCII 0x248 ~c#ebg1hc~ #8   // Override script
WRITE_EVALUATED_ASCII 0x260 ~~ #8   // General script

/* add script blocks to bd0120.bcs and bd0130.bcs in case group revisits Korlasz crypt later. */
EXTEND_BOTTOM ~bd0120.bcs~ ~c#endlessbg1/korlasz_dungeon_bg1/switch_dlgs/switch_npc_to_sod.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~bd0130.bcs~ ~c#endlessbg1/korlasz_dungeon_bg1/switch_dlgs/switch_npc_to_sod.baf~ EVALUATE_BUFFER

/* change back to BG1 ones */
/* we just patch this to the cutscne that teleports the group out of the dungeon */
EXTEND_TOP ~c#st2bc.bcs~ ~c#endlessbg1/korlasz_dungeon_bg1/switch_dlgs/switch_npc_to_bg1_cutscene.baf~ EVALUATE_BUFFER



/* Imoen should not be present twice: deactivate her elsewhere if she is also in Korlasz' Dungeon */
<<<<<<<< .../imoen_deactivate.baf
IF
	Global("C#EBG1_ImoenIsInKD","GLOBAL",1)
	!InParty("%IMOEN_DV%")
	Global("C#EBG1_DeactivateImoen","MYAREA",0)
THEN
	RESPONSE #100
        	Deactivate(Myself)
		SetGlobal("C#EBG1_DeactivateImoen","MYAREA",1)
END
IF
	Global("C#EBG1_ImoenIsInKD","GLOBAL",2)
	!InParty("%IMOEN_DV%")
	Global("C#EBG1_DeactivateImoen","MYAREA",1)
THEN
	RESPONSE #100
        	Activate(Myself)
		SetGlobal("C#EBG1_DeactivateImoen","MYAREA",2)
END
>>>>>>>>
EXTEND_BOTTOM ~%IMOEN_BCS%.bcs~ ~.../imoen_deactivate.baf~ EVALUATE_BUFFER 

<<<<<<<< .../ar0700_deactivate.baf
/* deactivate travel trigger */
IF
	OR(2)
		Global("C#EBG1_KorlaszQuest","GLOBAL",0)
		Global("C#EBG1_KorlaszQuest","GLOBAL",99)
	Global("C#EBG1_ChangeTriggerKD","MYAREA",0)
THEN
	RESPONSE #100
		TriggerActivation("c#ebg1korlasz",TRUE)
		TriggerActivation("C#EBG1_TranBD0120",FALSE)
		SetGlobal("C#EBG1_ChangeTriggerKD","MYAREA",1)
END

/* activate travel trigger */
IF
	GlobalGT("C#EBG1_KorlaszQuest","GLOBAL",0)
	GlobalLT("C#EBG1_KorlaszQuest","GLOBAL",99)
	GlobalLT("C#EBG1_ChangeTriggerKD","MYAREA",2)
THEN
	RESPONSE #100
		TriggerActivation("c#ebg1korlasz",FALSE)
		TriggerActivation("C#EBG1_TranBD0120",TRUE)
		AddMapNote([%mapnote_coordinates%],@31)
		SetGlobal("C#EBG1_ChangeTriggerKD","MYAREA",2)
END

/* Increase quest variable if Player1 leaves Korlasz' Dungeon after finishing the quest so cres inside bd0120.are will be gone */
IF
	Global("C#EBG1_KorlaszQuest","GLOBAL",2)
	InMyArea(Player1)
THEN
	RESPONSE #100
		SetGlobal("BDSH_Imoen_Floor","GLOBAL",4)
        	SetGlobal("C#EBG1_KorlaszQuest","GLOBAL",3)
END

/* extra variable to toggle Imoen */
IF
	Global("C#st_ImoenInGroupKD","GLOBAL",0)
	Global("C#EBG1_ImoenIsInKD","GLOBAL",1)
	GlobalGT("C#EBG1_KorlaszQuest","GLOBAL",2)
THEN
	RESPONSE #100
        	SetGlobal("C#EBG1_ImoenIsInKD","GLOBAL",2)
END
>>>>>>>>
OUTER_SPRINT ~mapnote_coordinates~ ~1090.3180~
EXTEND_BOTTOM ~%CentralBaldursGate%.bcs~ ~.../ar0700_deactivate.baf~ EVALUATE_BUFFER 

/* we repeat all in Docks area, also in case Player changes area too quickly */
OUTER_SPRINT ~mapnote_coordinates~ ~1090.108~
EXTEND_BOTTOM ~%BaldursGateDocks%.bcs~ ~.../ar0700_deactivate.baf~ EVALUATE_BUFFER 


/* SoD part: Ironthrone HQ area bd0050 */
/* travel trigger should only be available if Korlasz' Dungeon was done in BG1 */
<<<<<<<< .../bd0050_traveltrigger.baf
/* deactivate travel trigger for new SoD game */
IF
	Global("C#EBG1_KorlaszQuest","GLOBAL",0)
	Global("C#EBG1_ChangeTriggerKD","MYAREA",0)
THEN
	RESPONSE #100
		TriggerActivation("C#EBG1_TranBD0120",FALSE)
		TriggerActivation("c#ebg1korlasz",FALSE)
		SetGlobal("C#EBG1_ChangeTriggerKD","MYAREA",1)
END

/* set Mapnote */
IF
	GlobalGT("C#EBG1_KorlaszQuest","GLOBAL",0)
	Global("C#EBG1_AddMapNoteKD","MYAREA",0)
THEN
	RESPONSE #100
		AddMapNote([1200.160],@31)
		SetGlobal("C#EBG1_AddMapNoteKD","MYAREA",1)
END
>>>>>>>>
EXTEND_BOTTOM ~bd0050.bcs~ ~.../bd0050_traveltrigger.baf~ EVALUATE_BUFFER

/* failsafe: set variables in Palace area, too */
<<<<<<<< .../ar0108_failsafe.baf
/* Increase quest variable if Player1 leaves Korlasz' Dungeon after finishing the quest so cres inside bd0120.are will be gone */
IF
	Global("C#EBG1_KorlaszQuest","GLOBAL",2)
	InMyArea(Player1)
THEN
	RESPONSE #100
		SetGlobal("BDSH_Imoen_Floor","GLOBAL",4)
        	SetGlobal("C#EBG1_KorlaszQuest","GLOBAL",3)
END

/* extra variable to toggle Imoen */
IF
	Global("C#st_ImoenInGroupKD","GLOBAL",0)
	Global("C#EBG1_ImoenIsInKD","GLOBAL",1)
	GlobalGT("C#EBG1_KorlaszQuest","GLOBAL",2)
THEN
	RESPONSE #100
        	SetGlobal("C#EBG1_ImoenIsInKD","GLOBAL",2)
END
>>>>>>>>
EXTEND_BOTTOM ~%NBaldursGate_DucalPalace_L1%.bcs~ ~.../ar0108_failsafe.baf~ EVALUATE_BUFFER 

/* Place Korlasz into palace prison */
<<<<<<<< .../ar0111_korlasz.baf
IF
	GlobalGT("C#EBG1_KorlaszQuest","GLOBAL",1)
	GlobalLT("C#EBG1_KorlaszQuest","GLOBAL",99)
	Global("BD_KORLASZ_SURRENDER","GLOBAL",1)
	Global("C#EBG1_KorlaszInPrison","MYAREA",0)
	!Dead("bdkorlas")
THEN
	RESPONSE #100
		AddMapNoteColor([1030.300],%eet_2%67722,SLATE)  // Korlasz's Cell
		CreateCreature("BDKORLAS",[1003.298],SW)  // Korlasz
		ActionOverride("bdkorlas",DestroyAllEquipment())
        	SetGlobal("C#EBG1_KorlaszInPrison","MYAREA",1)
END
>>>>>>>>
EXTEND_BOTTOM ~%NBaldursGate_DucalPalace_Cellar%.bcs~ ~.../ar0111_korlasz.baf~ EVALUATE_BUFFER 

/* extra cutscene for transition to SoD: we need more actions, including handling of Imoen's stuff. */
<<<<<<<< .../c#st2so2.baf
/* move the group to some area so Imoen's stuff can be handled. I'll take the PC's room for now. (There is an Imoen in bd0103 already so we can't do it there.) */
IF
	AreaCheck("%NBaldursGate_DucalPalace_L1%")
THEN
	RESPONSE #100
        	CutSceneId(Player1)
		FadeToColor([1.0],0)
		SaveGame(2)
		SmallWait(1)
		ActionOverride(Player2,LeaveAreaLUA("%NBaldursGate_DucalPalace_L3%","",[315.1390],S))
		ActionOverride(Player3,LeaveAreaLUA("%NBaldursGate_DucalPalace_L3%","",[315.1390],S))
		ActionOverride(Player4,LeaveAreaLUA("%NBaldursGate_DucalPalace_L3%","",[315.1390],S))
		ActionOverride(Player5,LeaveAreaLUA("%NBaldursGate_DucalPalace_L3%","",[315.1390],S))
		ActionOverride(Player6,LeaveAreaLUA("%NBaldursGate_DucalPalace_L3%","",[315.1390],S))
		LeaveAreaLUAPanic("%NBaldursGate_DucalPalace_L3%","",[315.1390],SW)
		LeaveAreaLUA("%NBaldursGate_DucalPalace_L3%","",[315.1390],SW)
		MultiPlayerSync()
		Wait(1)		
		StartCutSceneEx("c#st2so2",TRUE)
END

/* skip Imoen handling if she was left for dead (because then the cre hasn't any items, anyway) */
IF
	AreaCheck("%NBaldursGate_DucalPalace_L3%")
	!InPartyAllowDead("%IMOEN_DV%")
	!InMyArea("%IMOEN_DV%")
	Dead("%IMOEN_DV%")
	Global("C#EBG1_MoveImoenStuff","MYAREA",0)
THEN
	RESPONSE #100
        	CutSceneId(Player1)
		SetGlobal("C#EBG1_MoveImoenStuff","MYAREA",3)
		StartCutSceneEx("c#st2so2",TRUE)
END

/* get Imoen in case she's not here */
IF
	AreaCheck("%NBaldursGate_DucalPalace_L3%")
	!InPartyAllowDead("%IMOEN_DV%")
	!InMyArea("%IMOEN_DV%")
	!Dead("%IMOEN_DV%")
	Global("C#EBG1_MoveImoenStuff","MYAREA",0)
THEN
	RESPONSE #100
        	CutSceneId(Player1)
		SetGlobal("C#EBG1_MoveImoenStuff","MYAREA",1)
		MoveGlobal("%NBaldursGate_DucalPalace_L3%","%IMOEN_DV%",[1068.763])
		SmallWait(10)
		StartCutSceneEx("c#st2so2",TRUE)
END

/* if in group, let her leave the group */
IF
	AreaCheck("%NBaldursGate_DucalPalace_L3%")
	InPartyAllowDead("%IMOEN_DV%")
	InMyArea("%IMOEN_DV%")	
	Global("C#EBG1_MoveImoenStuff","MYAREA",0)
THEN
	RESPONSE #100
        	CutSceneId(Player1)
		SetGlobal("C#EBG1_MoveImoenStuff","MYAREA",1)
		ActionOverride("%IMOEN_DV%",LeaveParty())
		SmallWait(10)
		StartCutSceneEx("c#st2so2",TRUE)
END

/* for al cases: move her stuff into the chest and let her disappear */
IF
	AreaCheck("%NBaldursGate_DucalPalace_L3%")
	InMyArea("%IMOEN_DV%")
	GlobalLT("C#EBG1_MoveImoenStuff","MYAREA",2)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
        	CutSceneId(Player1)
		SetGlobal("C#EBG1_MoveImoenStuff","MYAREA",2)
		ActionOverride("Container 4",TakeCreatureItems("%IMOEN_DV%",ALL)) //BACKPACK
		Wait(1)
		StartCutSceneEx("c#st2so2",TRUE)
END

/* for al cases: let Imoen disappear */
IF
	AreaCheck("%NBaldursGate_DucalPalace_L3%")
	InMyArea("%IMOEN_DV%")
	Global("C#EBG1_MoveImoenStuff","MYAREA",2)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
        	CutSceneId(Player1)
		SetGlobal("C#EBG1_MoveImoenStuff","MYAREA",3)
		ActionOverride("%IMOEN_DV%",DestroySelf())
		SmallWait(10)
		StartCutSceneEx("c#st2so2",TRUE)
END

/* after Imoen is done, start transition to SoD */
IF
	AreaCheck("%NBaldursGate_DucalPalace_L3%")	
	Global("C#EBG1_MoveImoenStuff","MYAREA",3)
THEN
	RESPONSE #100
        	CutSceneId(Player1)
		SetGlobal("C#EBG1_MoveImoenStuff","MYAREA",4)
		SetGlobal("bd_npc_camp_chapter","global",0)
		SetGlobal("chapter","global",7)
		SetGlobal("DREAM","GLOBAL",7)
		SetGlobal("sprite_is_deadsafana","GLOBAL",0)
		SetGlobal("sprite_is_deaddynaheir","GLOBAL",0)
		SetGlobal("sprite_is_deadminsc","GLOBAL",0)
		SetGlobal("sprite_is_deadrasaad","GLOBAL",0)
		TakeObjectGoldGlobal("BD_TAKEN_GOLD","GLOBAL",Player1)
		SetGlobal("BD_EXTRA_GOLD","GLOBAL",17)
		SetGlobal("BD_SAFEHOUSE_DONE","GLOBAL",1)
		ChangeAIScript("bdplayer",OVERRIDE)
		SmallWait(2)
		StartCutSceneEx("BDSODTRN",TRUE)
END
>>>>>>>>
COMPILE EVALUATE_BUFFER ~.../c#st2so2.baf~ 

/* cross-compatibility with component "Sarevok's Sword": we add script to remove Sarevok's Sword to BG1 area, too */
ACTION_IF (MOD_IS_INSTALLED ~c#endlessbg1.tp2~ ~4~) BEGIN

<<<<<<<< .../ar0110_remove_Sarevoks_sword.baf
/* Sarevok's Sword will be stolen after PC returns from Korlasz' Dungeon */
IF
  GlobalGT("bd_plot","GLOBAL",1)
  Global("C#st_RemoveSwordPCChest","MYAREA",0)
  HasItem("c#stsrvs","Container1")
THEN
  RESPONSE #100
    ActionOverride("Container1",DestroyItem("c#stsrvs"))
    SetGlobal("C#st_RemoveSwordPCChest","MYAREA",1)
END
>>>>>>>>
EXTEND_BOTTOM ~%NBaldursGate_DucalPalace_L3%.bcs~ ~.../ar0110_remove_Sarevoks_sword.baf~ EVALUATE_BUFFER 
END //SoD+EET

////////////////////////////////////////////////////////////////////////
/* Fenster the Palace Healer Is in the Palace (SoD/EET Only) */
////////////////////////////////////////////////////////////////////////
BEGIN @5026 DESIGNATED 15 
LABEL EBG1_FensterInPalace
REQUIRE_COMPONENT ~c#endlessbg1.tp2~ ~0~ @5003
REQUIRE_PREDICATE (GAME_INCLUDES ~sod~) @5012
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5023
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5023

/* Create cre for Duke Eltan in Palace after Sarevok's Death */
<<<<<<<< .../inlined/cre_patching.tph
>>>>>>>>
/* Fenster */
OUTER_SET strref_34426 = eet_200000 + 34426
APPEND_OUTER - ~.../inlined/cre_patching.tph~ 
"COPY_EXISTING ~bdwave36.cre~ ~override/C#STFENS.cre~
  WRITE_EVALUATED_ASCII 0x248 ~C#STFENS~ #8   // override script
  WRITE_EVALUATED_ASCII 0x250 ~SHOUT~ #8   // class script
  WRITE_EVALUATED_ASCII 0x258 ~~ #8   // race script
  WRITE_EVALUATED_ASCII 0x268 ~WTASIGHT~ #8   // default script
  WRITE_EVALUATED_ASCII 0x2cc ~C#STFENS~ #8   // dialogue
  WRITE_EVALUATED_ASCII 0x280 ~C#STFENS~ #32   // script name
WRITE_BYTE 0x2c 35 // Metal color
WRITE_BYTE 0x2d 46 // Minor color
WRITE_BYTE 0x2e 63 // Major color
WRITE_BYTE 0x2f 12 // Skin color
WRITE_BYTE 0x30 63 // Leather color
WRITE_BYTE 0x31 30 // Armor color
WRITE_BYTE 0x32 0 // Hair color
WRITE_LONG 0x270 128 // Allegiance
SAY NAME1 #%strref_34426% //Fenster
SAY NAME2 #%strref_34426%
REMOVE_CRE_ITEM ~unihelm SHLD06 HELM01~

/* create store for Fenster */
COPY_EXISTING ~bdff1000.sto~ ~override/C#STFENS.sto~
SAY STORE_NAME #%strref_34426% //Fenster"
INCLUDE ~.../inlined/cre_patching.tph~

<<<<<<<< .../c#ebg1_c#stfens.d
BEGIN C#STFENS
IF ~Global("C#EBG1_MetFenster","GLOBAL",0)~ THEN greetings
SAY @25 /* ~Greetings, hero. I am Fenster, the palace cleric.~ */
IF ~~ THEN DO ~SetGlobal("C#EBG1_MetFenster","GLOBAL",1)~ + greetings_01
END

IF ~Global("C#EBG1_MetFenster","GLOBAL",1)~ THEN greetings_01
SAY @26 /* ~How can I be of service?~ */
++ @27 /* ~Let me see what you have.~ */ + greetings_02
++ @28 /* ~Nothing right now.~ */ EXIT
END

IF ~~ THEN greetings_02
SAY @29 /* ~With pleasure.~ */
IF ~~ THEN DO ~StartStore("C#STFENS",LastTalkedToBy(Myself))~ EXIT
END

I_C_T bdfenste 0 C#EBG1_bdfenste_0
== bdfenste IF ~Global("C#EBG1_MetFenster","GLOBAL",1)~ THEN @30 /* ~I am not available for services while I tend to Imoen. Please ask for divine help elsewhere.~ */
END
>>>>>>>>
COMPILE EVALUATE_BUFFER ~.../c#ebg1_c#stfens.d~

<<<<<<<< .../ar0108_fenster.baf
/* Palace ground floor. Place Fenster */

IF
	Dead("Sarevok")
	!Exists("BDFENSTE")
	!Exists("C#STFENS")
	Global("C#st_PlaceFenster","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("C#st_PlaceFenster","MYAREA",1)
		CreateCreature("C#STFENS",[1148.619],W)
		SmallWait(1)
		Continue()
END

/* Remove in SoD/BGII */
IF
	OR(2) GlobalGT("ENDOFBG1","GLOBAL",0) GlobalGT("bd_plot","global",40)
	Exists("C#STFENS")
	GlobalLT("C#st_PlaceFenster","MYAREA",2)
THEN
	RESPONSE #100
		ActionOverride("C#STFENS",DestroySelf())
		SmallWait(1)
		SetGlobal("C#st_PlaceFenster","MYAREA",2)
		Continue()
END
>>>>>>>>
EXTEND_BOTTOM ~%NBaldursGate_DucalPalace_L1%.bcs~ ~.../ar0108_fenster.baf~ EVALUATE_BUFFER 

////////////////////////////////////////////////////////////////////////
/* Captain Corwin Is in the Palace (SoD/EET Only) */
////////////////////////////////////////////////////////////////////////
BEGIN @5027 DESIGNATED 16 
LABEL EBG1_CorwinInPalace
REQUIRE_COMPONENT ~c#endlessbg1.tp2~ ~0~ @5003
REQUIRE_PREDICATE (GAME_INCLUDES ~sod~) @5012
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5023
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5023

<<<<<<<< .../c#ebg1_c#stcorw.d
BEGIN C#STCORW
IF ~RandomNum(6,1)~ THEN greetings_01
SAY #%eet_2%64654 /* They call you the hero of Baldur's Gate. Try to act the part. */
IF ~~ THEN EXIT
END

IF ~RandomNum(6,2)~ THEN greetings_02
SAY #%eet_2%57277 /* Everything all right? */
IF ~~ THEN EXIT
END

IF ~RandomNum(6,3)~ THEN greetings_03
SAY #%eet_2%57316 /* Dung, desperation, and impotent rage. That's the smell of Flaming Fist justice. */
IF ~~ THEN EXIT
END

IF ~RandomNum(6,4)~ THEN greetings_04
SAY #%eet_2%52794 /* I'll be here if you need me. Try not to get into any trouble. */
IF ~~ THEN EXIT
END

IF ~RandomNum(6,5)~ THEN greetings_05
SAY #%eet_2%52804 /* Sometimes things go your way, sometimes they don't. Such is life. */
IF ~~ THEN EXIT
END

IF ~RandomNum(6,6)~ THEN greetings_06
SAY #%eet_2%35379 /* I'll be ready when it counts. */
IF ~~ THEN EXIT
END
>>>>>>>>
COMPILE EVALUATE_BUFFER ~.../c#ebg1_c#stcorw.d~

/* Create cre for Duke Eltan in Palace after Sarevok's Death */
COPY_EXISTING ~bdschael.cre~ ~override/C#STCORW.cre~
  WRITE_EVALUATED_ASCII 0x248 ~C#STCORW~ #8   // override script
  WRITE_EVALUATED_ASCII 0x250 ~SHOUT~ #8   // class script
  WRITE_EVALUATED_ASCII 0x258 ~~ #8   // race script
  WRITE_EVALUATED_ASCII 0x268 ~WTASIGHT~ #8   // default script
  WRITE_EVALUATED_ASCII 0x2cc ~C#STCORW~ #8   // dialogue
  WRITE_EVALUATED_ASCII 0x280 ~C#STCORW~ #32   // script name

<<<<<<<< .../ar0108_corwin.baf
/* Palace ground floor. Place Corwin */

IF
	Dead("Sarevok")
	!Exists("BDSCHAEL")
	!Exists("C#STCORW")
	Global("C#st_PlaceCorwin","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("C#st_PlaceCorwin","MYAREA",1)
		CreateCreature("C#STCORW",[656.261],S)
		SmallWait(1)
		Continue()
END

/* Remove in SoD/BGII */
IF
	OR(2) GlobalGT("ENDOFBG1","GLOBAL",0) GlobalGT("bd_plot","global",40)
	Exists("C#STFENS")
	GlobalLT("C#st_PlaceCorwin","MYAREA",2)
THEN
	RESPONSE #100
		ActionOverride("C#STCORW",DestroySelf())
		SmallWait(1)
		SetGlobal("C#st_PlaceCorwin","MYAREA",2)
		Continue()
END
>>>>>>>>
EXTEND_BOTTOM ~%NBaldursGate_DucalPalace_L1%.bcs~ ~.../ar0108_corwin.baf~ EVALUATE_BUFFER 