/* more flavor to PC being Hero of Baldur's Gate (includes Residence in the Palace) */

/* scripts and helper cre for PC's "bed" */
<<<<<<<< .../inlined/c#ebg1bd.baf
/* script of PC's "bed" */
IF
	Clicked([ANYONE])
	Global("C#st_HeroOfBG","GLOBAL",1)  
THEN
	RESPONSE #100
		CreateCreatureImpassable("c#ebg1bd",[234.386],0) 
END

IF
	Clicked([ANYONE])
THEN
	RESPONSE #100
		DisplayStringHead(Myself,@350)
END

>>>>>>>>
COMPILE EVALUATE_BUFFER ~.../inlined/c#ebg1bd.baf~ USING ~c#endlessbg1/translations/%s/dialogues.tra~

<<<<<<<< .../inlined/c#ebg1b2.baf
/* script for bed helper cre */
IF
	See([PC])
	Global("C#EBG1_TalkToPC","LOCALS",0)
THEN
	RESPONSE #100
		StartDialogNoSet([PC])
END

IF
	OR(2)
		!See([PC])
		Global("C#EBG1_TalkToPC","LOCALS",1)
THEN
	RESPONSE #100
		DestroySelf()
END
>>>>>>>>
COMPILE EVALUATE_BUFFER ~.../inlined/c#ebg1b2.baf~ 

/* Helper cre for PC's bed */
COPY ~c#endlessbg1/pc_palaceresidence/c#ebg1bd.cre~ ~override/c#ebg1bd.cre~
    SAY NAME1 @32 // ~<CHARNAME>'s Bed~
    SAY NAME2 @32 // ~<CHARNAME>'s Bed~
    WRITE_ASCII 0x280 ~c#ebg1bd~ #32 // set DV
    WRITE_ASCII 0x2cc ~c#ebg1bd~ #8 // set dialogue file
    WRITE_EVALUATED_ASCII 0x248 ~c#ebg1b2~ #8   // Override script  

/* add trigger point to PC's bed */
/* add info trigger for closed entrance */
COPY_EXISTING ~%NBaldursGate_DucalPalace_L3%.are~ ~override~
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 1    //trigger
    fj_box_left     = 163  //kleinstes x
    fj_box_top      = 299  //kleinstes y
    fj_box_right    = 305  //größtes x
    fj_box_bottom   = 456   //größtes y
    fj_cursor_idx   = 22 //question mark
    fj_vertex_0     = 250 + (456 << 16)   
    fj_vertex_1     = 305 + (419 << 16)   
    fj_vertex_2     = 305 + (358 << 16)   
    fj_vertex_3     = 219 + (299 << 16)   
    fj_vertex_4     = 163 + (340 << 16)   
    fj_vertex_5     = 164 + (395 << 16)   
    STR_VAR
    fj_structure_type   = region
    fj_name             = c#ebg1pcsbed
    fj_reg_script   = c#ebg1bd
  END
BUT_ONLY

/* PC servant on the top floor of the palace and mapnote */

COPY_EXISTING ~servan.cre~ ~override/c#stsrv1.cre~
    WRITE_ASCII 0x280 ~c#stsrv1~ #32 // set DV
    WRITE_ASCII 0x2cc ~c#stsrv1~ #8 // set dialogue file
    WRITE_EVALUATED_ASCII 0x258 ~INITDLG~ #8   // Race script

<<<<<<<< .../ar0110_add_extended.baf

IF
	Global("C#st_HeroOfBG","GLOBAL",1)
	Global("C#st_SpawnServant","MYAREA",0)
THEN
	RESPONSE #100
        	CreateCreature("c#stsrv1",[491.308],6)
		AddMapNote([80.440],@9)
		SetGlobal("C#st_SpawnServant","MYAREA",1)
END

IF
	OR(2) GlobalGT("ENDOFBG1","GLOBAL",0) GlobalGT("bd_plot","global",40)
	Exists("c#stsrv1")
	GlobalLT("C#st_SpawnServant","MYAREA",2)
THEN
	RESPONSE #100
		ActionOverride("c#stsrv1",DestroySelf())
		SmallWait(1)
		SetGlobal("C#st_SpawnServant","MYAREA",2)
END
>>>>>>>>
EXTEND_BOTTOM ~%NBaldursGate_DucalPalace_L3%.bcs~ ~.../ar0110_add_extended.baf~ EVALUATE_BUFFER 

/* SoD: Erase journal entries after leaving BG */
ACTION_IF ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~)) THEN BEGIN
<<<<<<<< .../bd1000_add_pc.baf

IF
	Global("C#st_EraseJournalHeroBG","bd1000",0)
THEN
	RESPONSE #100
		SetGlobal("C#st_EraseJournalHeroBG","bd1000",1)
		EraseJournalEntry(@10002)
		EraseJournalEntry(@10003)
END
>>>>>>>>
EXTEND_BOTTOM ~bd1000.bcs~ ~.../bd1000_add_pc.baf~ EVALUATE_BUFFER 
END //SoD(+EET)

/* N Baldurs Gate: spawn guards in front of the Palace */

COPY_EXISTING ~flambrid.cre~ ~override/c#stsrg1.cre~
    WRITE_ASCII 0x280 ~c#stsrg1~ #32 // set DV
    WRITE_ASCII 0x2cc ~c#stsrg1~ #8 // set dialogue file

COPY_EXISTING ~flambrid.cre~ ~override/c#stsrg2.cre~
    WRITE_ASCII 0x280 ~c#stsrg2~ #32 // set DV
    WRITE_ASCII 0x2cc ~c#stsrg1~ #8 // set dialogue file

<<<<<<<< .../ar0200_add_extended.baf
IF
	Dead("Sarevok")
	Global("C#st_SarevokDeadGuards","MYAREA",0)
THEN
	RESPONSE #100
        	CreateCreature("c#stsrg1",[1890.2746],0)
        	CreateCreature("c#stsrg2",[2069.2776],0)
		SetGlobal("C#st_SarevokDeadGuards","MYAREA",1)
END
>>>>>>>>
EXTEND_BOTTOM ~%NBaldursGate%.bcs~ ~.../ar0200_add_extended.baf~ EVALUATE_BUFFER 

ACTION_IF NOT GAME_IS ~bgt~ THEN BEGIN
/* 2nd floor of Palace: move container content from 3rd level to here */
<<<<<<<< .../ar0109_container_pc.baf
/* empty stuff out of PC's containers into level 2 drawer */
IF
  Global("C#st_HeroOfBG","GLOBAL",1)
  !Global("#L_BG1SarevokDead","GLOBAL",1)
  Global("C#st_PrepareContainersPC", "MYAREA", 0)
THEN
  RESPONSE #100
    MoveContainerContents("%NBaldursGate_DucalPalace_L3%*Container1","%NBaldursGate_DucalPalace_L2%*Container2")
    MoveContainerContents("%NBaldursGate_DucalPalace_L3%*Container2","%NBaldursGate_DucalPalace_L2%*Container2")
    MoveContainerContents("%NBaldursGate_DucalPalace_L3%*Container3","%NBaldursGate_DucalPalace_L2%*Container2")
    SetGlobal("C#st_PrepareContainersPC", "MYAREA", 1)
END
>>>>>>>>
EXTEND_BOTTOM ~%NBaldursGate_DucalPalace_L2%.bcs~ ~.../ar0109_container_pc.baf~ EVALUATE_BUFFER 
END //not bgt


/* prepare 3d floor of palace for the hero of Baldur's Gate */
/* can rest indoors */
COPY_EXISTING "%NBaldursGate_DucalPalace_L3%.are" "override"	
	WRITE_SHORT 0x48 (THIS BOR BIT7)	// This command will flag the area as "Can rest indoors" without editing existing flags (if any...)
BUT_ONLY

/* PC is not a thief if they open their chests */
  COPY_EXISTING ~ACT03.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~Opened(\[ANYONE\])~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~\(%textToReplace%\)~ ~\1 OR(2) !AreaCheck("%NBaldursGate_DucalPalace_L3%") Global("C#st_HeroOfBG","GLOBAL",0)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
  BUT_ONLY

<<<<<<<< .../ar0110_container_pc.baf
/* unlock PC'scontainer */
IF
	Global("C#st_HeroOfBG","GLOBAL",1)
	Global("C#st_UnlockContainersPC","MYAREA",0)
THEN
	RESPONSE #100
		Unlock("Container 1") 
		SetGlobal("C#st_UnlockContainersPC","MYAREA",1)
END
>>>>>>>>
EXTEND_BOTTOM ~%NBaldursGate_DucalPalace_L3%.bcs~ ~.../ar0110_container_pc.baf~ EVALUATE_BUFFER 

/* transfer content of PC's container into Palace in SoD */
ACTION_IF ((GAME_IS ~eet~) OR (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~)) THEN BEGIN
<<<<<<<< .../bd0103_container_pc.baf
  // Transferring items from BG1 area palace containers
IF
  Global("C#st_HeroOfBG","GLOBAL",1)
  Global("C#st_HeroTransferItemsPC", "MYAREA", 0)
THEN
  RESPONSE #100
    MoveContainerContents("%NBaldursGate_DucalPalace_L3%*Container1","BD0103*PlayerChest00")
    SetGlobal("C#st_HeroTransferItemsPC", "MYAREA", 1)
END
>>>>>>>>
EXTEND_BOTTOM ~bd0103.bcs~ ~.../bd0103_container_pc.baf~ EVALUATE_BUFFER 
END //SoD+EET
